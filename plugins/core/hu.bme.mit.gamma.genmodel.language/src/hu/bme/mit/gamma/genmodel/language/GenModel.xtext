/********************************************************************************
 * Copyright (c) 2018 Contributors to the Gamma project
 *
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * SPDX-License-Identifier: EPL-1.0
 ********************************************************************************/
grammar hu.bme.mit.gamma.genmodel.language.GenModel with hu.bme.mit.gamma.expression.language.ExpressionLanguage

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.mit.bme.hu/gamma/statechart/Model/Statechart" as statechart
import "http://www.mit.bme.hu/gamma/statechart/Model/Interface" as interface
import "http://www.mit.bme.hu/gamma/statechart/Model/Composite" as composite
import "http://www.mit.bme.hu/gamma/genmodel/Model"
import "http://www.mit.bme.hu/gamma/trace/Model" as trace
import "http://www.mit.bme.hu/gamma/property/Model" as property
import "http://www.mit.bme.hu/gamma/xSTS/Model" as xsts

import "http://www.yakindu.org/sct/sgraph/2.0.0" as sgraph
import "http://www.yakindu.org/sct/statechart/SText" as stext
import "http://www.yakindu.org/base/types/2.0.0" as types

GenModel returns GenModel:
	(('import' 
		(packageImports+=[interface::Package | STRING] |
		'external' statechartImports+=[sgraph::Statechart| STRING] |
		'trace' traceImports+=[trace::ExecutionTrace | STRING])) |
		'include' genmodelImports+=[GenModel | STRING]
	)*
	(tasks += Task)*
;

Task returns Task:
	YakinduCompilation | CodeGeneration | AnalysisModelTransformation | TestGeneration |
		EventPriorityTransformation | AdaptiveContractTestGeneration | PhaseStatechartGeneration |
		Verification | TestReplayModelGeneration | Slicing
;

YakinduCompilation returns YakinduCompilation:
	StatechartCompilation | InterfaceCompilation
;

InterfaceCompilation returns InterfaceCompilation:
	'interface' '{'
		'statechart' ':' statechart=[sgraph::Statechart]
		(
			'folder' ':' targetFolder+=STRING | // The location of the folder relative to the project
			'file' ':' fileName+=STRING | // The name of the generated file
			'package' ':' packageName+=STRING // Gamma package name
		)*
	'}'
;

StatechartCompilation returns StatechartCompilation:
	'statechart'  '{'
		'statechart' ':' statechart=[sgraph::Statechart]
		(
			'folder' ':' targetFolder+=STRING | // The location of the folder relative to the project
			'file' ':' fileName+=STRING | // The name of the generated file
			'package' ':' packageName+=STRING | // Gamma package name
			'name' ':' statechartName+=STRING | // Gamma statechart name
			'mappings' '{'
				(interfaceMappings+=InterfaceMapping)*
			'}'
		)*
	'}'
;

CodeGeneration returns CodeGeneration:
	'code'  '{'
		'component' ':' component=[interface::Component]
		(
			'folder' ':' targetFolder+=STRING |
			'base-package' ':' packageName+=STRING | // Java base package
			'language' ':' language+=ProgrammingLanguage
		)*
	'}'
;

TestGeneration returns TestGeneration:
	'test' '{'
		'trace' ':' executionTrace=[trace::ExecutionTrace]
		(
			'folder' ':' targetFolder+=STRING |
			'file' ':' fileName+=STRING | // The name of the generated file
			'base-package' ':' packageName+=STRING | // Java base package
			'language' ':' language+=ProgrammingLanguage
		)*
	'}'
;

EventPriorityTransformation returns EventPriorityTransformation:
	'event-priority' '{'
		'statechart' ':' statechart=[statechart::StatechartDefinition]
		(
			'folder' ':' targetFolder+=STRING |
			'file' ':' fileName+=STRING // The name of the generated file
		)*
	'}'
;

enum ProgrammingLanguage returns ProgrammingLanguage:
	JAVA = 'java'
;

AnalysisModelTransformation returns AnalysisModelTransformation:
	'analysis' '{'
		model=ModelReference
		(
			'folder' ':' targetFolder+=STRING |			
			'file' ':' fileName+=STRING |
			'language' ':' languages+=AnalysisLanguage |
			'property-file' ':' propertyPackage=[property::PropertyPackage | STRING] | // Property models
			'minimal-element-set' ':' ('false' | minimalElementSet?='true') |
			'scheduler' ':' scheduler+=Scheduler |
			'constraint' ':' constraint=Constraint |
			coverages+=Coverage
		)*
	'}'
;

ModelReference returns ModelReference:
	ComponentReference | XSTSReference
;

ComponentReference returns ComponentReference:
	'component' ':' component=[interface::Component] ('(' arguments+=Expression (',' arguments+=Expression)* ')')? 
;

XSTSReference returns XSTSReference:
	'xsts' ':' xSts=[xsts::XSTS | STRING]
;

Constraint returns Constraint:
	'{' (SchedulingConstraint | OrchestratingConstraint) '}'
;

SchedulingConstraint returns SchedulingConstraint: 
	(instanceConstraint+=AsynchronousInstanceConstraint)+
;

ComponentInstanceReference returns composite::ComponentInstanceReference:
	componentInstanceHierarchy+=[composite::ComponentInstance] ('->' componentInstanceHierarchy+=[composite::ComponentInstance] )*
;

ComponentInstancePortReference returns property::ComponentInstancePortReference:
	instance=ComponentInstanceReference '.' port=[interface::Port]
;

ComponentInstanceStateConfigurationReference returns property::ComponentInstanceStateConfigurationReference:
	instance=ComponentInstanceReference '.' region=[statechart::Region] '.' state=[statechart::State] 
;

ComponentInstanceTransitionReference returns property::ComponentInstanceTransitionReference:
	instance=ComponentInstanceReference '.' transition=[statechart::TransitionIdAnnotation]
;

AsynchronousInstanceConstraint returns AsynchronousInstanceConstraint:
	(instance=ComponentInstanceReference | 'top') ':' '{'
		orchestratingConstraint=OrchestratingConstraint
	'}'
;

OrchestratingConstraint returns OrchestratingConstraint:
	'minimum-orchestrating-period' ':' minimumPeriod=TimeSpecification
	'maximum-orchestrating-period' ':' maximumPeriod=TimeSpecification
;

TimeSpecification returns interface::TimeSpecification:
	value=AdditiveExpression unit=TimeUnit
;

enum TimeUnit returns interface::TimeUnit:
	MILLISECOND = 'ms' | SECOND = 's'
;

enum Scheduler returns Scheduler:
	RANDOM = "random" //| FAIR = "fair"
;

enum AnalysisLanguage returns AnalysisLanguage:
	UPPAAL = "UPPAAL" | THETA = "Theta" | XSTS_UPPAAL = "XSTS-UPPAAL"
;

Coverage returns Coverage:
	StateCoverage | TransitionCoverage | TransitionPairCoverage | OutEventCoverage | InteractionCoverage
;

StateCoverage returns StateCoverage:
	{StateCoverage} 'state-coverage' ('{' 
		('include' ':' '[' include+=ComponentInstanceReference (',' include+=ComponentInstanceReference)* ']')?
		('exclude' ':' '[' exclude+=ComponentInstanceReference (',' exclude+=ComponentInstanceReference)* ']')?
	'}')?
;

TransitionCoverage returns TransitionCoverage:
	{TransitionCoverage} 'transition-coverage' ('{' 
		('include' ':' '[' include+=ComponentInstanceReference (',' include+=ComponentInstanceReference)* ']')?
		('exclude' ':' '[' exclude+=ComponentInstanceReference (',' exclude+=ComponentInstanceReference)* ']')?
	'}')?
;

TransitionPairCoverage returns TransitionPairCoverage:
	{TransitionPairCoverage} 'transition-pair-coverage' ('{' 
		('include' ':' '[' include+=ComponentInstanceReference (',' include+=ComponentInstanceReference)* ']')?
		('exclude' ':' '[' exclude+=ComponentInstanceReference (',' exclude+=ComponentInstanceReference)* ']')?
	'}')?
;

OutEventCoverage returns OutEventCoverage:
	{OutEventCoverage} 'out-event-coverage' ('{' 
		('include' ':' '[' include+=ComponentInstanceReference (',' include+=ComponentInstanceReference)* ']')?
		('exclude' ':' '[' exclude+=ComponentInstanceReference (',' exclude+=ComponentInstanceReference)* ']')?
		('port-include' ':' '[' portInclude+=ComponentInstancePortReference (',' portInclude+=ComponentInstancePortReference)* ']')?
		('port-exclude' ':' '[' portExclude+=ComponentInstancePortReference (',' portExclude+=ComponentInstancePortReference)* ']')?
	'}')?
;

InteractionCoverage returns InteractionCoverage:
	{InteractionCoverage} 'interaction-coverage' ('{'
		('sender-coverage-criterion' ':' senderCoverageCriterion=InteractionCoverageCriterion)?
		('receiver-coverage-criterion' ':' receiverCoverageCriterion=InteractionCoverageCriterion)?
		('include' ':' '[' include+=ComponentInstanceReference (',' include+=ComponentInstanceReference)* ']')?
		('exclude' ':' '[' exclude+=ComponentInstanceReference (',' exclude+=ComponentInstanceReference)* ']')?
		('port-include' ':' '[' portInclude+=ComponentInstancePortReference (',' portInclude+=ComponentInstancePortReference)* ']')?
		('port-exclude' ':' '[' portExclude+=ComponentInstancePortReference (',' portExclude+=ComponentInstancePortReference)* ']')?
		('state-include' ':' '[' stateInclude+=ComponentInstanceStateConfigurationReference (',' stateInclude+=ComponentInstanceStateConfigurationReference)* ']')?
		('state-exclude' ':' '[' stateExclude+=ComponentInstanceStateConfigurationReference (',' stateExclude+=ComponentInstanceStateConfigurationReference)* ']')?
		('transition-include' ':' '[' transitionInclude+=ComponentInstanceTransitionReference (',' transitionInclude+=ComponentInstanceTransitionReference)* ']')?
		('transition-exclude' ':' '[' transitionExclude+=ComponentInstanceTransitionReference (',' transitionExclude+=ComponentInstanceTransitionReference)* ']')?
	'}')?
;

enum InteractionCoverageCriterion returns InteractionCoverageCriterion:
	EVERY_INTERACTION = 'every-interaction' | STATES_AND_EVENTS = 'states-and-events' | EVENTS = 'events'
;

Verification returns Verification:
	{Verification} 'verification' '{'
		(
			'trace-folder' ':' targetFolder+=STRING |	// Trace folder
			'file' ':' fileName+=STRING | // Analysis model
			'language' ':' languages+=AnalysisLanguage |
			'query-file' ':' queryFiles+=STRING | // Queries
			'property-file' ':' propertyPackages+=[property::PropertyPackage | STRING] | // Property models
			'base-package' ':' packageName+=STRING  | // Java base package
			'test-language' ':' language+=ProgrammingLanguage |
			'optimize' ':' ('false' | optimize?='true') |
			'test-folder' ':' testFolder+=STRING
		)*
	'}'
;

TestReplayModelGeneration returns TestReplayModelGeneration:
	{TestReplayModelGeneration} 'test-replay' '{'
		'trace' ':' executionTrace=[trace::ExecutionTrace]
		(
			'system-file' ':' fileName+=STRING | // The name of the file containing the system
			'folder' ':' targetFolder+=STRING
		)*
	'}'
;

Slicing returns Slicing:
	{Slicing} 'slicing' '{'
		'property' ':' propertyPackage=[property::PropertyPackage | STRING]
		(
			'file' ':' fileName+=STRING | // The name of the generated file
			'folder' ':' targetFolder+=STRING
		)*
	'}'
;


InterfaceMapping returns InterfaceMapping:
	'map' yakinduInterface=[stext::InterfaceScope] '=' gammaInterface=[interface::Interface] 'as' realizationMode=RealizationMode ('{'
		eventMappings+=EventMapping*
	'}')?
;

EventMapping returns EventMapping:
	'map' yakinduEvent=[types::Event] '=' gammaEvent=[interface::Event] 
;

enum RealizationMode returns interface::RealizationMode:
	PROVIDED = 'provided' | REQUIRED = 'required'
;

AdaptiveContractTestGeneration returns AdaptiveContractTestGeneration:
	{AdaptiveContractTestGeneration} 'adaptive-test' '{'
		'statechart' ':' statechartContract=[statechart::StatechartDefinition] ('(' arguments+=Expression (',' arguments+=Expression)* ')')? 
		(
		'folder' ':' targetFolder+=STRING |
		'file' ':' fileName+=STRING | // The name of the generated file
		'base-package' ':' packageName+=STRING | // Java base package
		'language' ':' language+=ProgrammingLanguage
		)*
	'}'
;

PhaseStatechartGeneration returns PhaseStatechartGeneration:
	{PhaseStatechartGeneration} 'phase-statechart' '{'
		'statechart' ':' statechart=[statechart::StatechartDefinition] ('(' arguments+=Expression (',' arguments+=Expression)* ')')? 
		(
		'folder' ':' targetFolder+=STRING |
		'file' ':' fileName+=STRING // The name of the generated file
		)*
	'}'
;

