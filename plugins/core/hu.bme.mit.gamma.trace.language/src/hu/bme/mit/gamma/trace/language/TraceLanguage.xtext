/********************************************************************************
 * Copyright (c) 2018-2023 Contributors to the Gamma project
 *
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * SPDX-License-Identifier: EPL-1.0
 ********************************************************************************/
grammar hu.bme.mit.gamma.trace.language.TraceLanguage with hu.bme.mit.gamma.expression.language.ExpressionLanguage

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.mit.bme.hu/gamma/trace/Model" 
import "http://www.mit.bme.hu/gamma/expression/Model" as ConstraintModel
import "http://www.mit.bme.hu/gamma/statechart/Model/Statechart" as StatechartModel
import "http://www.mit.bme.hu/gamma/statechart/Model/Composite" as CompositeModel
import "http://www.mit.bme.hu/gamma/statechart/Model/Interface" as InterfaceModel

ExecutionTrace returns ExecutionTrace:
	'import' ^import=[InterfaceModel::Package | STRING]
	 annotations+=ExecutionTraceAnnotations*
	'trace' name=ID 'of' (component=[InterfaceModel::Component] ('(' arguments+=Expression (',' arguments+=Expression)* ')')?)
	(variableDeclarations+=VariableDeclaration)*
	steps+=Step
	(steps+=Step)*
	(cycle=Cycle)?
;

ExecutionTraceAnnotations returns ExecutionTraceAnnotation:
	AllowedWaitingAnnotation | NegativeTestAnnotation | ExecutionTraceCommentAnnotation |
			TimeUnitAnnotation
;

NegativeTestAnnotation returns NegativeTestAnnotation:
	{NegativeTestAnnotation}
	'@NegativeTest'
;

AllowedWaitingAnnotation returns ExecutionTraceAllowedWaitingAnnotation:
	'@AllowedWaiting' lowerLimit=Expression '..' upperLimit=Expression
;

ExecutionTraceCommentAnnotation returns ExecutionTraceCommentAnnotation:
	'@' '(' comment=STRING ')'
;

TimeUnitAnnotation returns TimeUnitAnnotation:
	'@TimeUnit' '=' timeUnit=TimeUnit
;

enum TimeUnit returns InterfaceModel::TimeUnit:
	NANOSECOND = 'ns' | MICROSECOND = 'Î¼s' | MILLISECOND = 'ms' | SECOND = 's' | HOUR = 'h'
;

Cycle returns Cycle:
	'cycle' '{'
		(steps+=Step)+
	'}'
;

Step returns Step:
	{Step}
	'step' '{'
		('act' '{'
			(actions+=Act)+
		'}')?
		'assert' '{'
			(asserts+=Expression ';')*
		'}'
	'}'
;

Act returns Act:
	Reset | RaiseEventAct | TimeElapse | AssignmentAct | InstanceSchedule | ComponentSchedule;
	
Reset returns Reset:
	{Reset} 'reset'
;

RaiseEventAct returns RaiseEventAct:
	'raise' port=[InterfaceModel::Port] '.' event=[InterfaceModel::Event] '('
		(arguments+=LiteralExpression)? (',' arguments+=LiteralExpression)* ')'
;

TimeElapse returns TimeElapse:
	'elapse' elapsedTime=Expression
;

AssignmentAct returns AssignmentAct:
	'assign' lhs=AssignableAccessExpression ':=' rhs=Expression
;

// Same as in the action language

AssignableAccessExpression returns ConstraintModel::ReferenceExpression:
	AssignablePrimaryExpression(
		{ConstraintModel::ArrayAccessExpression.operand=current} '[' index=Expression ']' |
		{ConstraintModel::RecordAccessExpression.operand=current} '.' fieldReference=FieldReferenceExpression
	)* 
;

AssignablePrimaryExpression returns ConstraintModel::ReferenceExpression: 
	AssignableDirectReferenceExpression | 
	'(' AssignableAccessExpression ')'
;

AssignableDirectReferenceExpression returns ConstraintModel::ReferenceExpression:
	{ConstraintModel::DirectReferenceExpression} declaration=[ConstraintModel::ValueDeclaration]
;

//

InstanceSchedule returns InstanceSchedule:
	'schedule' instanceReference=ComponentInstanceReferenceExpression
;

ComponentSchedule returns ComponentSchedule:
	{ComponentSchedule}
	'schedule' 'component'
;

@Override
ReferenceExpression returns ConstraintModel::Expression:
	RaiseEventAct | EventParameterReferenceExpression |
	ComponentInstanceStateReferenceExpression | ComponentInstanceVariableReferenceExpression |
	DirectReferenceExpression
;

EventParameterReferenceExpression returns InterfaceModel::EventParameterReferenceExpression:
	'parameter' port=[InterfaceModel::Port] '.' event=[InterfaceModel::Event] '::' parameter=[ConstraintModel::ParameterDeclaration]
;

ComponentInstanceVariableReferenceExpression returns CompositeModel::ComponentInstanceVariableReferenceExpression:
	'variable' (instance=ComponentInstanceReferenceExpression | 'self') '.' variableDeclaration=[ConstraintModel::VariableDeclaration]
;

ComponentInstanceStateReferenceExpression returns CompositeModel::ComponentInstanceStateReferenceExpression:
	'state' (instance=ComponentInstanceReferenceExpression | 'self') '.' region=[StatechartModel::Region] '.' state=[StatechartModel::State]
;

ComponentInstanceReferenceExpression returns CompositeModel::ComponentInstanceReferenceExpression:
	componentInstance=[CompositeModel::ComponentInstance] ('->' child=ComponentInstanceReferenceExpression)?
;
