/********************************************************************************
 * Copyright (c) 2018-2023 Contributors to the Gamma project
 *
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * SPDX-License-Identifier: EPL-1.0
 ********************************************************************************/
grammar hu.bme.mit.gamma.statechart.language.StatechartLanguage 
	with hu.bme.mit.gamma.action.language.ActionLanguage

import "http://www.eclipse.org/emf/2002/Ecore" as Ecore
import "http://www.mit.bme.hu/gamma/statechart/Model/Statechart" 
import "http://www.mit.bme.hu/gamma/expression/Model" as ExpressionModel 
import "http://www.mit.bme.hu/gamma/action/Model" as ActionModel 
import "http://www.mit.bme.hu/gamma/statechart/Model/Composite" as CompositeModel
import "http://www.mit.bme.hu/gamma/statechart/Model/Interface" as InterfaceModel 
import "http://www.mit.bme.hu/gamma/statechart/Model/Contract" as ContractModel 
import "http://www.mit.bme.hu/gamma/statechart/Model/Phase" as PhaseModel 

Package returns InterfaceModel::Package:
	'package' name=ID
	('import' imports+=[InterfaceModel::Package | STRING])*	
	(
		constantDeclarations+=ConstantDeclaration |
		functionDeclarations+=FunctionDeclaration |
		typeDeclarations+=TypeDeclaration |
		components+=Component |
		interfaces+=Interface
	)*
;

TimeoutDeclaration returns TimeoutDeclaration:
	'timeout' name=ID
;

enum And returns BinaryType:
	AND = '&&'
;

enum Or returns BinaryType:
	OR = '||'
;

enum Equal returns BinaryType:
	EQUAL = '=='
;

enum Xor returns BinaryType:
	XOR = '^'
;

enum Imply returns BinaryType:
	IMPLY = '->'
;

Trigger returns InterfaceModel::Trigger:
	OnCycleTrigger |
	AnyTrigger |
	ComplexTrigger
;

/**
 * Used by ControlSpecification.
 * The other trigger rules have precedence rules and defined according to left recursion.
 */
SimpleTrigger returns InterfaceModel::SimpleTrigger:
	OnCycleTrigger |
	AnyTrigger |
	OpaqueTrigger |
	EventTrigger
;

ComplexTrigger returns InterfaceModel::Trigger:
	BinaryTrigger 
;

BinaryTrigger returns InterfaceModel::Trigger:
	EqualTrigger
;

EqualTrigger returns InterfaceModel::Trigger:
	ImplyTrigger ({BinaryTrigger.leftOperand=current} type=Equal rightOperand=EqualTrigger)?
;

ImplyTrigger returns InterfaceModel::Trigger:
	OrTrigger ({BinaryTrigger.leftOperand=current} type=Imply rightOperand=ImplyTrigger)?
;

OrTrigger returns InterfaceModel::Trigger:
	XorTrigger ({BinaryTrigger.leftOperand=current} type=Or rightOperand=OrTrigger)?
;

XorTrigger returns InterfaceModel::Trigger:
	AndTrigger ({BinaryTrigger.leftOperand=current} type=Xor rightOperand=XorTrigger)?
;

AndTrigger returns InterfaceModel::Trigger:
	BinaryOperand ({BinaryTrigger.leftOperand=current} type=And rightOperand=AndTrigger)?
;

BinaryOperand returns InterfaceModel::Trigger:
	UnaryTrigger | ParenthesesTrigger
;

enum Not returns UnaryType:
	NOT='!'
;

UnaryTrigger returns InterfaceModel::Trigger:
	NotTrigger | EventTrigger | OpaqueTrigger 
;

NotTrigger returns UnaryTrigger:
	type=Not operand=ParenthesesTrigger
;

ParenthesesTrigger returns InterfaceModel::Trigger:
	'('EqualTrigger')'
;

OnCycleTrigger returns OnCycleTrigger:
	{OnCycleTrigger} 'cycle'
;

AnyTrigger returns InterfaceModel::AnyTrigger:
	{InterfaceModel::AnyTrigger} 'any'
;

OpaqueTrigger returns OpaqueTrigger:
	trigger=STRING
;

EventTrigger returns InterfaceModel::EventTrigger:
	eventReference=EventReference
;

EventReference returns InterfaceModel::EventReference:
	ClockTickReference | 
	TimeoutEventReference |
	AnyPortEventReference | 
	PortEventReference
;

ClockTickReference returns ClockTickReference:
	clock=[InterfaceModel::Clock]
;

TimeoutEventReference returns TimeoutEventReference:
	'timeout' timeout=[TimeoutDeclaration]
;

AnyPortEventReference returns AnyPortEventReference:
	port=[InterfaceModel::Port] '.' 'any'
;

PortEventReference returns PortEventReference:
	port=[InterfaceModel::Port] '.' event=[InterfaceModel::Event]
;

Port returns InterfaceModel::Port:
	'port' name=ID ':' interfaceRealization=InterfaceRealization ('{' (('invariant' invariants+=Expression)((',')? 'invariant' invariants+=Expression)*)? '}')?
;

InterfaceRealization returns InterfaceModel::InterfaceRealization:
	realizationMode=RealizationMode interface=[InterfaceModel::Interface]
;

enum RealizationMode returns InterfaceModel::RealizationMode:
	PROVIDED = 'provides' | REQUIRED = 'requires'
;

SynchronousStatechartDefinition returns SynchronousStatechartDefinition:
	(
		('@RegionSchedule' '=' schedulingOrder=SchedulingOrder)? &
		('@OrthogonalRegionSchedule' '=' orthogonalRegionSchedulingOrder=OrthogonalRegionSchedulingOrder)? &
		('@TransitionPriority' '=' transitionPriority=TransitionPriority)? &
		('@GuardEvaluation' '=' guardEvaluation=GuardEvaluation)? &
		(	annotations+=ComponentAnnotation |
			annotations+=StatechartContractAnnotation
		)*
	)
	'statechart' name=ID 
	('(' ((parameterDeclarations+=ParameterDeclaration)(',' parameterDeclarations+=ParameterDeclaration)*)? ')')?
		('[' ((ports+=Port)((',')? ports+=Port)*)? ']')? '{'
			(
				variableDeclarations+=VariableDeclaration |
				timeoutDeclarations+=TimeoutDeclaration |
				('invariant' invariants+=Expression)
			)*
			(
				regions+=Region |
				transitions+=Transition
			)*
			functionDeclarations+=FunctionDeclaration*
	'}'
;

AsynchronousStatechartDefinition returns AsynchronousStatechartDefinition:
	'@Asynchronous'
	(
		('@RegionSchedule' '=' schedulingOrder=SchedulingOrder)? &
		('@OrthogonalRegionSchedule' '=' orthogonalRegionSchedulingOrder=OrthogonalRegionSchedulingOrder)? &
		('@TransitionPriority' '=' transitionPriority=TransitionPriority)? &
		('@GuardEvaluation' '=' guardEvaluation=GuardEvaluation)? &
		(	annotations+=ComponentAnnotation |
			annotations+=StatechartContractAnnotation
		)*
	)
	'statechart' name=ID ('capacity' '(' capacity=Expression ')')?
	('(' ((parameterDeclarations+=ParameterDeclaration)(',' parameterDeclarations+=ParameterDeclaration)*)? ')')?
		('[' ((ports+=Port)((',')? ports+=Port)*)? ']')? '{'
			(
				variableDeclarations+=VariableDeclaration |
				timeoutDeclarations+=TimeoutDeclaration |
				('invariant' invariants+=Expression)
			)*
			(
				regions+=Region |
				transitions+=Transition
			)*
			functionDeclarations+=FunctionDeclaration*
	'}'
;

enum SchedulingOrder returns SchedulingOrder:
	TOP_DOWN = 'top-down' | BOTTOM_UP = 'bottom-up'
;

enum OrthogonalRegionSchedulingOrder returns OrthogonalRegionSchedulingOrder:
	SEQUENTIAL = 'sequential' | UNORDERED = 'unordered' | PARALLEL = 'parallel'
;

enum TransitionPriority returns TransitionPriority:
	OFF = 'off' | ORDER_BASED = 'order-based' | VALUE_BASED = 'value-based'
;

enum GuardEvaluation returns GuardEvaluation:
	ON_THE_FLY = 'on-the-fly' | BEGINNING_OF_STEP = 'beginning-of-step'
;

ComponentAnnotation returns InterfaceModel::ComponentAnnotation: 
	WrapperComponentAnnotation | RunUponExternalEventAnnotation | RunUponExternalEventOrInternalTimeoutAnnotation
;

StatechartContractAnnotation returns StatechartAnnotation:
	ScenarioAllowedWaitingAnnotation | ScenarioContractAnnotation |
	AdaptiveContractAnnotation | MissionPhaseAnnotation | HasInitialoutputsBlockAnnotation | 
	NegativeContractStatechartAnnotation
;

NegativeContractStatechartAnnotation returns ContractModel::NegativeContractStatechartAnnotation:
	{ContractModel::NegativeContractStatechartAnnotation}
	'@NegativeContractStatechart'
;

HasInitialoutputsBlockAnnotation returns ContractModel::HasInitialOutputsBlockAnnotation:
	{ContractModel::HasInitialOutputsBlockAnnotation}
	'@HasInitialOutputsBlock'
;


enum SpecialStateKind returns ContractModel::SpecialStateKind:
	ACCEPTING = 'accepting'
;

ScenarioAllowedWaitingAnnotation returns ContractModel::ScenarioAllowedWaitAnnotation:
	{ContractModel::ScenarioAllowedWaitAnnotation}
	('@ScenarioAllowedWaitAnnotation' | '@ScenarioAllowedWait') '=' lowerLimit=Expression '..' upperLimit=Expression 
;

ScenarioContractAnnotation returns ContractModel::ScenarioContractAnnotation:
	'@ScenarioContractStatechart' '=' monitoredComponent=[InterfaceModel::Component] '(' scenarioType = NotDefinedEventMode ')'
;

enum NotDefinedEventMode returns ContractModel::NotDefinedEventMode: 
	PERMISSIVE = 'permissive' | STRICT ='strict'
;

AdaptiveContractAnnotation returns ContractModel::AdaptiveContractAnnotation:
	'@AdaptiveContractStatechart' '=' monitoredComponent=[InterfaceModel::Component]
;

MissionPhaseAnnotation returns PhaseModel::MissionPhaseAnnotation:
	{PhaseModel::MissionPhaseAnnotation} '@PhaseStatechart'
;

WrapperComponentAnnotation returns InterfaceModel::WrapperComponentAnnotation:
	{InterfaceModel::WrapperComponentAnnotation} '@WrapperComponent'
;

RunUponExternalEventAnnotation returns RunUponExternalEventAnnotation:
	{RunUponExternalEventAnnotation} ('@RunUponExternalEventAnnotation' |
		'@RunUponExternalEvent')
;

RunUponExternalEventOrInternalTimeoutAnnotation returns RunUponExternalEventOrInternalTimeoutAnnotation:
	{RunUponExternalEventOrInternalTimeoutAnnotation} ('@RunUponExternalEventOrInternalTimeoutAnnotation' |
		'@RunUponExternalEventOrInternalTimeout' )
;

Region returns Region:
	'region' name=ID '{'
		(stateNodes+=StateNode)*
	'}'
;

Transition returns Transition:
	(annotations+=TransitionAnnotation)*
	'transition' ('(' priority=INTEGER ')')?
	'from' sourceState=[StateNode] 'to' targetState=[StateNode]
	(
		('when' trigger=Trigger)?
		('[' guard=Expression ']')?
		('/' ( => effects+=Action)* )?
	)
;

TransitionAnnotation returns TransitionAnnotation:
	TransitionIdAnnotation
;

TransitionIdAnnotation returns TransitionIdAnnotation:
	'@' '(' name=ID ')'
;

StateNode returns StateNode:
	PseudoState |
	State
;

State returns State:
	(annotations+=StateAnnotation)*
	'state' name=ID
	(
		'{'
		(
			(
				('invariant' invariants+=Expression)*
				('entry' '/' (=> entryActions+=Action)* )?
				('exit' '/' (=> exitActions+=Action)* )?
			)
			(regions+=Region)*
		)
		'}'
	)?
;

StateAnnotation returns StateAnnotation:
	StateContractAnnotation | MissionPhaseStateAnnotation | SpecialStateAnnotation
;

SpecialStateAnnotation returns ContractModel::SpecialStateAnnotation :
	{ContractModel::SpecialStateAnnotation}
	('@SpecialStateAnnotation' | '@SpecialState') kind=SpecialStateKind
;

StateContractAnnotation returns ContractModel::StateContractAnnotation:
	'@StatechartContract' (linkType=LinkType)? (hasHistory?='history')?
		'=' contractStatechart=[StatechartDefinition] ('(' arguments+=Expression (',' arguments+=Expression)* ')')?
;

enum LinkType returns ContractModel::LinkType:
	TO_CONTROLLER = 'self' | TO_COMPONENT = 'component' // DEFAULT is set by default
;

MissionPhaseStateAnnotation returns PhaseModel::MissionPhaseStateAnnotation:
	{PhaseModel::MissionPhaseStateAnnotation} '@Phase' '=' '['
		(history=History component=ComponentInstance '{'
			portBindings+=PortBinding*
			variableBindings+=VariableBinding*
		'}')
	']'
;

VariableBinding returns PhaseModel::VariableBinding:
	('tie' statechartVariable=[ExpressionModel::VariableDeclaration] '->'
		instanceVariableReference=InstanceVariableReference) |
	('bind' statechartVariable=[ExpressionModel::VariableDeclaration] '='
		instanceVariableReference=InstanceVariableReference)
;

InstanceVariableReference returns PhaseModel::InstanceVariableReference:
	instance=[CompositeModel::ComponentInstance] '.' variable=[ExpressionModel::VariableDeclaration]
;

enum History returns PhaseModel::History:
	NO_HISTORY = 'no-history' | SHALLOW_HISTORY = 'shallow' | DEEP_HISTORY = 'deep'
;

PseudoState returns PseudoState:
	EntryState |
	ForkState |
	JoinState |
	ChoiceState |
	MergeState
;

MergeState returns MergeState:
	'merge' name=ID
;

ChoiceState returns ChoiceState:
	'choice' name=ID
;

JoinState returns JoinState:
	'join' name=ID
;

ForkState returns ForkState:
	'fork' name=ID
;

EntryState:
	InitialState |
	ShallowHistoryState |
	DeepHistoryState
;

InitialState returns InitialState:
	'initial' name=ID
;

ShallowHistoryState returns ShallowHistoryState:
	'shallow' 'history' name=ID
;

DeepHistoryState returns DeepHistoryState:
	'deep' 'history' name=ID
;

// Composition language 
 
Component returns InterfaceModel::Component:
	SynchronousComponent |
	AsynchronousComponent
;

SynchronousComponent returns CompositeModel::SynchronousComponent:
	SynchronousStatechartDefinition |
	AbstractSynchronousComponent
;

AbstractSynchronousComponent returns CompositeModel::AbstractSynchronousCompositeComponent:
	CascadeCompositeComponent |
	SynchronousCompositeComponent
;

CascadeCompositeComponent returns CompositeModel::CascadeCompositeComponent:
	annotations+=ComponentAnnotation*
	'cascade' name=ID 
	('(' ((parameterDeclarations+=ParameterDeclaration)(',' parameterDeclarations+=ParameterDeclaration)*)? ')')?
		('[' ((ports+=Port)((',')? ports+=Port)*)? ']')?
		'{'
			(
				components+=SynchronousComponentInstance |
				portBindings+=PortBinding |
				channels+=Channel
			)*
			('initial' 'execute' initialExecutionList+=AtomicComponentInstanceReferenceExpression
					(',' initialExecutionList+=AtomicComponentInstanceReferenceExpression)*)?
			('execute' executionList+=AtomicComponentInstanceReferenceExpression
					(',' executionList+=AtomicComponentInstanceReferenceExpression)*)?
		'}'
;

SynchronousCompositeComponent returns CompositeModel::SynchronousCompositeComponent:
	annotations+=ComponentAnnotation*
	'sync' name=ID 
	('(' ((parameterDeclarations+=ParameterDeclaration)(',' parameterDeclarations+=ParameterDeclaration)*)? ')')?
		('[' ((ports+=Port)((',')? ports+=Port)*)? ']')?
		'{'			
			(
				components+=SynchronousComponentInstance |
				portBindings+=PortBinding |
				channels+=Channel
			)*
		'}'
;

AsynchronousComponent returns CompositeModel::AsynchronousComponent:
	AbstractAsynchronousCompositeComponent |
	AsynchronousAdapter |
	AsynchronousStatechartDefinition
;

AbstractAsynchronousCompositeComponent returns CompositeModel::AbstractAsynchronousCompositeComponent:
	AsynchronousCompositeComponent |
	ScheduledAsynchronousCompositeComponent
;


AsynchronousCompositeComponent returns CompositeModel::AsynchronousCompositeComponent:
	annotations+=ComponentAnnotation*
	'async' name=ID 
	('(' ((parameterDeclarations+=ParameterDeclaration)(',' parameterDeclarations+=ParameterDeclaration)*)? ')')?
		('[' ((ports+=Port)((',')? ports+=Port)*)? ']')?
		'{'
			(
				components+=AsynchronousComponentInstance |
				portBindings+=PortBinding |
				channels+=Channel
			)*
		'}'
;

ScheduledAsynchronousCompositeComponent returns CompositeModel::ScheduledAsynchronousCompositeComponent:
	annotations+=ComponentAnnotation*
	'scheduled-async' name=ID 
	('(' ((parameterDeclarations+=ParameterDeclaration) (',' parameterDeclarations+=ParameterDeclaration)*)? ')')?
		('[' ((ports+=Port)((',')? ports+=Port)*)? ']')?
		'{'
			(
				components+=AsynchronousComponentInstance |
				portBindings+=PortBinding |
				channels+=Channel
			)*
			('initial' 'execute' initialExecutionList+=AtomicComponentInstanceReferenceExpression
					(',' initialExecutionList+=AtomicComponentInstanceReferenceExpression)*)?
			('execute' executionList+=AtomicComponentInstanceReferenceExpression
					(',' executionList+=AtomicComponentInstanceReferenceExpression)*)?
		'}'
;

AsynchronousAdapter returns CompositeModel::AsynchronousAdapter:
	annotations+=ComponentAnnotation*
	'adapter' name=ID ('(' ((parameterDeclarations+=ParameterDeclaration)
			(',' parameterDeclarations+=ParameterDeclaration)*)? ')')?
		'of' wrappedComponent=SynchronousComponentInstance
		('[' ((ports+=Port)((',')? ports+=Port)*)? ']')?
		'{'
			(
				clocks+=ClockDeclaration |
				controlSpecifications+=ControlSpecificaiton |
				messageQueues+=MessageQueue
			)*

		'}'
;

ClockDeclaration returns InterfaceModel::Clock:
	'clock' name=ID '(' 'rate' '=' timeSpecification=TimeSpecification')'
;

TimeSpecification returns InterfaceModel::TimeSpecification:
	value=AdditiveExpression unit=TimeUnit
;

@Override
LiteralExpression returns ExpressionModel::Expression: 
	super |
	('time' TimeSpecification)
;


enum TimeUnit returns InterfaceModel::TimeUnit:
	NANOSECOND = 'ns' | MICROSECOND = 'Î¼s' | MILLISECOND = 'ms' | SECOND = 's' | HOUR = 'h'
;

ControlSpecificaiton returns CompositeModel::ControlSpecification:
	 'when' trigger=SimpleTrigger '/' controlFunction=ControlFunction
;

enum ControlFunction returns CompositeModel::ControlFunction:
	RUN_ONCE = 'run' | RUN_TO_COMPLETION = 'full-step' | RESET = 'reset' | RESET_MESSAGE_QUEUE = 'reset-queue'
		| RESET_MESSAGE_QUEUES = 'reset-queues' | RESET_OTHER_MESSAGE_QUEUES = 'reset-other-queues' 
;

MessageQueue returns CompositeModel::MessageQueue:
	'queue' name=ID '(' ('priority' '=' priority=INTEGER) (',' 'capacity' '=' capacity=Expression)
				(',' 'discard' '=' eventDiscardStrategy=DiscardStrategy)?
			')' '{'
	 	(eventPassings+=EventPassing) (',' eventPassings+=EventPassing)*
	'}'
;

EventPassing returns CompositeModel::EventPassing:
	source=EventReference ('->' target=EventReference)?
;

enum DiscardStrategy returns CompositeModel::DiscardStrategy:
	INCOMING = 'incoming' | OLDEST = 'oldest'
;

PortBinding returns CompositeModel::PortBinding:
	'bind' compositeSystemPort=[InterfaceModel::Port] '->' instancePortReference=InstancePortReference
;

InstancePortReference returns CompositeModel::InstancePortReference:
	instance=[CompositeModel::ComponentInstance] '.' port=[InterfaceModel::Port] 
;

ComponentInstance returns CompositeModel::ComponentInstance:
	SynchronousComponentInstance | 'async' AsynchronousComponentInstance
;

AtomicComponentInstanceReferenceExpression returns CompositeModel::ComponentInstanceReferenceExpression:
	componentInstance=[CompositeModel::ComponentInstance]
;
// Subgrammars can use this
ComponentInstanceReferenceExpression returns CompositeModel::ComponentInstanceReferenceExpression:
	componentInstance=[CompositeModel::ComponentInstance] ('->' child=ComponentInstanceReferenceExpression )?
;

SynchronousComponentInstance returns CompositeModel::SynchronousComponentInstance:
	'component' name=ID ':' type=[CompositeModel::SynchronousComponent] ('(' arguments+=Expression (',' arguments+=Expression)* ')')?
;

AsynchronousComponentInstance returns CompositeModel::AsynchronousComponentInstance:
	'component' name=ID ':' type=[CompositeModel::AsynchronousComponent] ('(' arguments+=Expression (',' arguments+=Expression)* ')')?
;

Channel returns CompositeModel::Channel:
	SimpleChannel | BroadcastChannel
;

SimpleChannel returns CompositeModel::SimpleChannel:
	// This is the channel's point of view: input is the input of the channel, i.e. the out port signals of statecharts
	'channel' '[' providedPort=InstancePortReference ']' '-o)-' '[' requiredPort=InstancePortReference ']'
;

BroadcastChannel returns CompositeModel::BroadcastChannel:
	// This is the channel's point of view: input is the input of the channel, i.e. the out port signals of statecharts
	'channel' '[' providedPort=InstancePortReference ']' '-o)-' '[' (requiredPorts+=InstancePortReference) (',' requiredPorts+=InstancePortReference)+ ']'
;

// Interface

Interface returns InterfaceModel::Interface:
	{InterfaceModel::Interface}
	'interface' name=ID ('extends' (parents+=[InterfaceModel::Interface]) (',' (parents+=[InterfaceModel::Interface]))* )? '{'
		((events+=EventDeclaration))*
		('invariant' invariants+=Expression)*
	'}'
;

enum EventDirection returns InterfaceModel::EventDirection:
	IN = 'in' | OUT = 'out' | INOUT = 'inout' | INTERNAL = 'internal'
;

EventDeclaration returns InterfaceModel::EventDeclaration:
	{InterfaceModel::EventDeclaration}
	(direction=EventDirection) (event=Event) 
;

Event returns InterfaceModel::Event:
	{InterfaceModel::Event}
	(persistency=Persistency)? 'event' name=ID ('(' (parameterDeclarations+=ParameterDeclaration) (','(parameterDeclarations+=ParameterDeclaration))* ')')? ('priority' '=' priority=INTEGER)?
;

enum Persistency returns InterfaceModel::Persistency:
	PERSISTENT = 'persistent' | TRANSIENT = 'transient'
;

// Overriding it for the sake of EventParameterReferenceExpression
@Override 
PrimaryExpression returns ExpressionModel::Expression: 
	super |
	EventParameterReferenceExpression |
	InterfaceParameterReferenceExpression |
	TimeoutReferenceExpression |
	( EventReference '::' ) | // Now these event references are not usable only in triggers
	StateReferenceExpression
;

StateReferenceExpression returns StateReferenceExpression:
	'in-state' '(' region=[Region] '.' state = [State] ')'
;

EventParameterReferenceExpression returns InterfaceModel::EventParameterReferenceExpression:
	port=[InterfaceModel::Port] '.' event=[InterfaceModel::Event] '::' parameter=[ExpressionModel::ParameterDeclaration];

InterfaceParameterReferenceExpression returns InterfaceModel::InterfaceParameterReferenceExpression:
	//this is a placeholder for the language to be unambiguous
	'this' '.' event=[InterfaceModel::Event] '::' parameter=[ExpressionModel::ParameterDeclaration];
	
TimeoutReferenceExpression returns TimeoutReferenceExpression:
	'time-elapsed' '(' timeout=[TimeoutDeclaration] ')'
;

// Action Language Extensions

@Override 
InlineStatement returns ActionModel::Statement:
	super |
	(
		(
		RaiseEventAction |
		SetTimeoutAction
		)
		';'
	)
;

RaiseEventAction returns RaiseEventAction:
	'raise' port=[InterfaceModel::Port] '.' event=[InterfaceModel::Event] ('(' (arguments+=Expression)(',' arguments+=Expression)* ')')?
;

SetTimeoutAction returns SetTimeoutAction:
	'set' timeoutDeclaration=[TimeoutDeclaration] ':=' time=TimeSpecification
;

DeactivateTimeoutAction returns DeactivateTimeoutAction:
	'deactivate' timeoutDeclaration=[TimeoutDeclaration]
;

// Component instance element references for subgrammars

ComponentInstanceStateExpression returns CompositeModel::ComponentInstanceElementReferenceExpression:
	ComponentInstanceStateReferenceExpression | ComponentInstanceVariableReferenceExpression |
		ComponentInstanceEventReferenceExpression | ComponentInstanceEventParameterReferenceExpression
;

ComponentInstanceStateReferenceExpression returns CompositeModel::ComponentInstanceStateReferenceExpression:
	'state' instance=ComponentInstanceReferenceExpression '.' region=[Region] '.' state=[State]
;

ComponentInstanceVariableReferenceExpression returns CompositeModel::ComponentInstanceVariableReferenceExpression:
	'variable' instance=ComponentInstanceReferenceExpression '.' variableDeclaration=[ExpressionModel::VariableDeclaration]
;

ComponentInstanceEventReferenceExpression returns CompositeModel::ComponentInstanceEventReferenceExpression:
	'event' instance=ComponentInstanceReferenceExpression '.' port=[InterfaceModel::Port] '.' event=[InterfaceModel::Event]
;

ComponentInstanceEventParameterReferenceExpression returns CompositeModel::ComponentInstanceEventParameterReferenceExpression:
	'parameter' instance=ComponentInstanceReferenceExpression '.' port=[InterfaceModel::Port] '.'
		event=[InterfaceModel::Event] '::' parameterDeclaration=[ExpressionModel::ParameterDeclaration]
;

ComponentInstanceReflectiveElementReferenceExpression returns CompositeModel::ComponentInstanceReflectiveElementReferenceExpression:
	'element' instance=ComponentInstanceReferenceExpression ('.' identifier+=ID) ('::' identifier+=ID)*
;

//