/********************************************************************************
 * Copyright (c) 2018 Contributors to the Gamma project
 *
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * SPDX-License-Identifier: EPL-1.0
 ********************************************************************************/
grammar hu.bme.mit.gamma.statechart.language.StatechartLanguage 
	with hu.bme.mit.gamma.action.language.ActionLanguage

import "http://www.eclipse.org/emf/2002/Ecore" as Ecore
import "http://www.mit.bme.hu/gamma/statechart/Model/Statechart" 
import "http://www.mit.bme.hu/gamma/expression/Model" as ConstraintModel 
import "http://www.mit.bme.hu/gamma/action/Model" as ActionModel 
import "http://www.mit.bme.hu/gamma/statechart/Model/Composite" as CompositeModel
import "http://www.mit.bme.hu/gamma/statechart/Model/Interface" as InterfaceModel 
import "http://www.mit.bme.hu/gamma/statechart/Model/Contract" as ContractModel 
import "http://www.mit.bme.hu/gamma/statechart/Model/Phase" as PhaseModel 

Package returns InterfaceModel::Package:
	'package' name=ID
	('import' imports+=[InterfaceModel::Package | STRING])*	
	(
		constantDeclarations+=ConstantDeclaration |
		functionDeclarations+=FunctionDeclaration |
		typeDeclarations+=TypeDeclaration |
		components+=Component |
		interfaces+=Interface
	)*
;

TimeoutDeclaration returns TimeoutDeclaration:
	'timeout' name=ID
;

enum And returns BinaryType:
	AND = '&&'
;

enum Or returns BinaryType:
	OR = '||'
;

enum Equal returns BinaryType:
	EQUAL = '=='
;

enum Xor returns BinaryType:
	XOR = '^'
;

enum Imply returns BinaryType:
	IMPLY = '->'
;

Trigger returns InterfaceModel::Trigger:
	OnCycleTrigger |
	AnyTrigger |
	ComplexTrigger
;

/**
 * Used by ControlSpecification.
 * The other trigger rules have precedence rules and defined according to left recursion.
 */
SimpleTrigger returns InterfaceModel::SimpleTrigger:
	OnCycleTrigger |
	AnyTrigger |
	OpaqueTrigger |
	EventTrigger
;

ComplexTrigger returns InterfaceModel::Trigger:
	BinaryTrigger 
;

BinaryTrigger returns InterfaceModel::Trigger:
	EqualTrigger
;

EqualTrigger returns InterfaceModel::Trigger:
	ImplyTrigger ({BinaryTrigger.leftOperand=current} type=Equal rightOperand=EqualTrigger)?
;

ImplyTrigger returns InterfaceModel::Trigger:
	OrTrigger ({BinaryTrigger.leftOperand=current} type=Imply rightOperand=ImplyTrigger)?
;

OrTrigger returns InterfaceModel::Trigger:
	XorTrigger ({BinaryTrigger.leftOperand=current} type=Or rightOperand=OrTrigger)?
;

XorTrigger returns InterfaceModel::Trigger:
	AndTrigger ({BinaryTrigger.leftOperand=current} type=Xor rightOperand=XorTrigger)?
;

AndTrigger returns InterfaceModel::Trigger:
	BinaryOperand ({BinaryTrigger.leftOperand=current} type=And rightOperand=AndTrigger)?
;

BinaryOperand returns InterfaceModel::Trigger:
	UnaryTrigger | ParenthesesTrigger
;

enum Not returns UnaryType:
	NOT='!'
;

UnaryTrigger returns InterfaceModel::Trigger:
	NotTrigger | EventTrigger | OpaqueTrigger 
;

NotTrigger returns UnaryTrigger:
	type=Not operand=ParenthesesTrigger
;

ParenthesesTrigger returns InterfaceModel::Trigger:
	'('EqualTrigger')'
;

OnCycleTrigger returns OnCycleTrigger:
	{OnCycleTrigger} 'cycle'
;

AnyTrigger returns InterfaceModel::AnyTrigger:
	{InterfaceModel::AnyTrigger} 'any'
;

OpaqueTrigger returns OpaqueTrigger:
	trigger=STRING
;

EventTrigger returns InterfaceModel::EventTrigger:
	eventReference=EventReference
;

EventReference returns InterfaceModel::EventReference:
	ClockTickReference | 
	TimeoutEventReference |
	AnyPortEventReference | 
	PortEventReference
;

ClockTickReference returns ClockTickReference:
	clock=[InterfaceModel::Clock]
;

TimeoutEventReference returns TimeoutEventReference:
	'timeout' timeout=[TimeoutDeclaration]
;

AnyPortEventReference returns AnyPortEventReference:
	port=[InterfaceModel::Port] '.' 'any'
;

PortEventReference returns PortEventReference:
	port=[InterfaceModel::Port] '.' event=[InterfaceModel::Event]
;

Port returns InterfaceModel::Port:
	'port' name=ID ':' interfaceRealization=InterfaceRealization
;

InterfaceRealization returns InterfaceModel::InterfaceRealization:
	realizationMode=RealizationMode interface=[InterfaceModel::Interface]
;

enum RealizationMode returns InterfaceModel::RealizationMode:
	PROVIDED = 'provides' | REQUIRED = 'requires'
;

StatechartDefinition returns StatechartDefinition:
	(
		('@RegionSchedule' '=' schedulingOrder=SchedulingOrder)? &
		('@OrthogonalRegionSchedule' '=' orthogonalRegionSchedulingOrder=OrthogonalRegionSchedulingOrder)? &
		('@TransitionPriority' '=' transitionPriority=TransitionPriority)? &
		('@GuardEvaluation' '=' guardEvaluation=GuardEvaluation)? &
		(annotations+=StatechartContractAnnotation)*
	)
	'statechart' name=ID 
	('(' ((parameterDeclarations+=ParameterDeclaration)(',' parameterDeclarations+=ParameterDeclaration)*)? ')')?
		('[' ((ports+=Port)((',')? ports+=Port)*)? ']')? '{'
			(
				variableDeclarations+=VariableDeclaration |
				timeoutDeclarations+=TimeoutDeclaration |
				transitions+=Transition
			)*
			regions+=Region
			(
				variableDeclarations+=VariableDeclaration |
				timeoutDeclarations+=TimeoutDeclaration |
				transitions+=Transition |
				regions+=Region
			)*
		(functionDeclarations+=FunctionDeclaration)*
	'}'
;

enum SchedulingOrder returns SchedulingOrder:
	TOP_DOWN = 'top-down' | BOTTOM_UP = 'bottom-up'
;

enum OrthogonalRegionSchedulingOrder returns OrthogonalRegionSchedulingOrder:
	SEQUENTIAL = 'sequential' | RANDOM = 'random'
;

enum TransitionPriority returns TransitionPriority:
	OFF = 'off' | ORDER_BASED = 'order-based' | VALUE_BASED = 'value-based'
;

enum GuardEvaluation returns GuardEvaluation:
	ON_THE_FLY = 'on-the-fly' | BEGINNING_OF_STEP = 'beginning-of-step'
;

StatechartContractAnnotation returns StatechartAnnotation:
	ScenarioAllowedWaitingAnnotation | ScenarioContractAnnotation | AdaptiveContractAnnotation | MissionPhaseAnnotation
;

ScenarioAllowedWaitingAnnotation returns ContractModel::ScenarioAllowedWaitAnnotation:
	{ContractModel::ScenarioAllowedWaitAnnotation}
	'@ScenarioAllowedWaitAnnotation' '=' lowerLimit=Expression '..' upperLimit=Expression 
;

ScenarioContractAnnotation returns ContractModel::ScenarioContractAnnotation:
	'@ScenarioContractStatechart' '=' monitoredComponent=[InterfaceModel::Component] '(' scenarioType = NotDefinedEventMode ')'
;

enum NotDefinedEventMode returns ContractModel::NotDefinedEventMode: 
	PERMISSIVE = 'permissive' | STRICT ='strict'
;


AdaptiveContractAnnotation returns ContractModel::AdaptiveContractAnnotation:
	'@AdaptiveContractStatechart' '=' monitoredComponent=[InterfaceModel::Component]
;

MissionPhaseAnnotation returns PhaseModel::MissionPhaseAnnotation:
	{PhaseModel::MissionPhaseAnnotation} '@PhaseStatechart'
;

StatechartReference returns StatechartReference:
	{StatechartReference}
	'reference' reference=[CompositeModel::SynchronousComponent] ('(' ((arguments+=Expression)(',' arguments+=Expression)*)? ')')?
;

Region returns Region:
	'region' name=ID '{'
		(stateNodes+=StateNode)*
	'}'
;

Transition returns Transition:
	(annotations+=TransitionAnnotation)*
	'transition' ('(' priority=INTEGER ')')?
	'from' sourceState=[StateNode] 'to' targetState=[StateNode]
	(
		('when' trigger=Trigger)?
		('[' guard=Expression ']')?
		('/' ( => effects+=Action)* )?
	)
;

TransitionAnnotation returns TransitionAnnotation:
	TransitionIdAnnotation
;

TransitionIdAnnotation returns TransitionIdAnnotation:
	'@''(' name=ID ')'
;

StateNode returns StateNode:
	State |
	PseudoState
;

State returns State:
	(annotation=StateAnnotation)?
	'state' name=ID
	(
		'{'
		(
			(
				('invariant' invariants+=Expression)*
				('entry' '/' (=> entryActions+=Action)* )?
				('exit' '/' (=> exitActions+=Action)* )?
			)
			(regions+=Region)*
		)
		'}'
	)?
;

StateAnnotation returns StateAnnotation:
	StateContractAnnotation | MissionPhaseStateAnnotation
;

StateContractAnnotation returns ContractModel::StateContractAnnotation:
	'@StatechartContract' '=' contractStatecharts+=[StatechartDefinition] (',' contractStatecharts+=[StatechartDefinition])*
;

MissionPhaseStateAnnotation returns PhaseModel::MissionPhaseStateAnnotation:
	{PhaseModel::MissionPhaseStateAnnotation} '@Phase' '=' '['
		stateDefinitions += StateDefinition*
	']'
;

StateDefinition returns PhaseModel::MissionPhaseStateDefinition:
	history=History component=SynchronousComponentInstance '{'
		portBindings+=PortBinding*
		variableBindings+=VariableBinding*
	'}'
;

VariableBinding returns PhaseModel::VariableBinding:
	'tie' statechartVariable=[ConstraintModel::VariableDeclaration] '->' instanceVariableReference=InstanceVariableReference
;

InstanceVariableReference returns PhaseModel::InstanceVariableReference:
	instance=[CompositeModel::SynchronousComponentInstance] '.' variable=[ConstraintModel::VariableDeclaration]
;

enum History returns PhaseModel::History:
	NO_HISTORY = 'no history' | SHALLOW_HISTORY = 'shallow' | DEEP_HISTORY = 'deep'
;

PseudoState returns PseudoState:
	EntryState |
	ForkState |
	JoinState |
	ChoiceState |
	MergeState
;

MergeState returns MergeState:
	'merge' name=ID
;

ChoiceState returns ChoiceState:
	'choice' name=ID
;

JoinState returns JoinState:
	'join' name=ID
;

ForkState returns ForkState:
	'fork' name=ID
;

EntryState:
	InitialState |
	ShallowHistoryState |
	DeepHistoryState
;

InitialState returns InitialState:
	'initial' name=ID
;

ShallowHistoryState returns ShallowHistoryState:
	'shallow' 'history' name=ID
;

DeepHistoryState returns DeepHistoryState:
	'deep' 'history' name=ID
;

// Composition language 
 
Component returns InterfaceModel::Component:
	SynchronousComponent |
	AsynchronousComponent
;

SynchronousComponent returns CompositeModel::SynchronousComponent:
	StatechartDefinition |
	AbstractSynchronousComponent
;

AbstractSynchronousComponent returns CompositeModel::AbstractSynchronousCompositeComponent:
	CascadeCompositeComponent |
	SynchronousCompositeComponent
;

CascadeCompositeComponent returns CompositeModel::CascadeCompositeComponent:
	'cascade' name=ID 
	('(' ((parameterDeclarations+=ParameterDeclaration)(',' parameterDeclarations+=ParameterDeclaration)*)? ')')?
		('[' ((ports+=Port)((',')? ports+=Port)*)? ']')?
		'{'
			(
				components+=SynchronousComponentInstance |
				portBindings+=PortBinding |
				channels+=Channel |
				'execute' executionList+=AtomicComponentInstanceReference
						(',' executionList+=AtomicComponentInstanceReference)*
			)*
		'}'
;

SynchronousCompositeComponent returns CompositeModel::SynchronousCompositeComponent:
	'sync' name=ID 
	('(' ((parameterDeclarations+=ParameterDeclaration)(',' parameterDeclarations+=ParameterDeclaration)*)? ')')?
		('[' ((ports+=Port)((',')? ports+=Port)*)? ']')?
		'{'			
			(
				components+=SynchronousComponentInstance |
				portBindings+=PortBinding |
				channels+=Channel
			)*
		'}'
;

AsynchronousComponent returns CompositeModel::AsynchronousComponent:
	AbstractAsynchronousCompositeComponent |
	AsynchronousAdapter
;

AbstractAsynchronousCompositeComponent returns CompositeModel::AbstractAsynchronousCompositeComponent:
	AsynchronousCompositeComponent |
	ScheduledAsynchronousCompositeComponent
;


AsynchronousCompositeComponent returns CompositeModel::AsynchronousCompositeComponent:
	'async' name=ID 
	('(' ((parameterDeclarations+=ParameterDeclaration)(',' parameterDeclarations+=ParameterDeclaration)*)? ')')?
		('[' ((ports+=Port)((',')? ports+=Port)*)? ']')?
		'{'
			(
				components+=AsynchronousComponentInstance |
				portBindings+=PortBinding |
				channels+=Channel
			)*
		'}'
;

ScheduledAsynchronousCompositeComponent returns CompositeModel::ScheduledAsynchronousCompositeComponent:
	'scheduled-async' name=ID 
	('(' ((parameterDeclarations+=ParameterDeclaration)(',' parameterDeclarations+=ParameterDeclaration)*)? ')')?
		('[' ((ports+=Port)((',')? ports+=Port)*)? ']')?
		'{'
			(
				components+=AsynchronousComponentInstance |
				portBindings+=PortBinding |
				channels+=Channel | 
				'execute' executionList+=AtomicComponentInstanceReference
						(',' executionList+=AtomicComponentInstanceReference)*
			)*
		'}'
;

AsynchronousAdapter returns CompositeModel::AsynchronousAdapter:
	'adapter' name=ID ('(' ((parameterDeclarations+=ParameterDeclaration)(',' parameterDeclarations+=ParameterDeclaration)*)? ')')?
		'of' wrappedComponent=SynchronousComponentInstance
		('[' ((ports+=Port)((',')? ports+=Port)*)? ']')?
		'{'
			(
				clocks+=ClockDeclaration |
				controlSpecifications+=ControlSpecificaiton |
				messageQueues+=MessageQueue
			)*

		'}'
;

ClockDeclaration returns InterfaceModel::Clock:
	'clock' name=ID '(' 'rate' '=' timeSpecification=TimeSpecification')'
;

TimeSpecification returns InterfaceModel::TimeSpecification:
	value=AdditiveExpression unit=TimeUnit
;

enum TimeUnit returns InterfaceModel::TimeUnit:
	MILLISECOND = 'ms' | SECOND = 's'
;	


ControlSpecificaiton returns CompositeModel::ControlSpecification:
	 'when' trigger=SimpleTrigger '/' controlFunction=ControlFunction
;

enum ControlFunction returns CompositeModel::ControlFunction:
	RUN_ONCE = 'run' | RUN_TO_COMPLETION = 'full step' | RESET = 'reset'
;

MessageQueue returns CompositeModel::MessageQueue:
	'queue' name=ID '('('priority' '=' priority=INTEGER) (',' 'capacity' '=' capacity=Expression)
			(
				(',' 'discard' '=' eventDiscardStrategy=DiscardStrategy)? &
				(',' 'retrieve-count' '=' messageRetrievalCount=Expression)?
			) ')' '{'
	 	(eventReference+=EventReference)(',' eventReference+=EventReference)*
	'}'
;

enum DiscardStrategy returns CompositeModel::DiscardStrategy:
	INCOMING = 'incoming' | OLDEST = 'oldest'
;

PortBinding returns CompositeModel::PortBinding:
	'bind' compositeSystemPort=[InterfaceModel::Port] '->' instancePortReference=InstancePortReference
;

InstancePortReference returns CompositeModel::InstancePortReference:
	instance=[CompositeModel::ComponentInstance] '.' port=[InterfaceModel::Port] 
;

ComponentInstance returns CompositeModel::ComponentInstance:
	SynchronousComponentInstance | AsynchronousComponentInstance
;

AtomicComponentInstanceReference returns CompositeModel::ComponentInstanceReference:
	componentInstance=[CompositeModel::ComponentInstance]
;
// Subgrammars can use this
ComponentInstanceReference returns CompositeModel::ComponentInstanceReference:
	componentInstance=[CompositeModel::ComponentInstance] ('->' child=ComponentInstanceReference )?
;

SynchronousComponentInstance returns CompositeModel::SynchronousComponentInstance:
	'component' name=ID ':' type=[CompositeModel::SynchronousComponent] ('(' arguments+=Expression (',' arguments+=Expression)* ')')?
;

AsynchronousComponentInstance returns CompositeModel::AsynchronousComponentInstance:
	'component' name=ID ':' type=[CompositeModel::AsynchronousComponent] ('(' arguments+=Expression (',' arguments+=Expression)* ')')?
;

Channel returns CompositeModel::Channel:
	SimpleChannel | BroadcastChannel
;

SimpleChannel returns CompositeModel::SimpleChannel:
	// This is the channel's point of view: input is the input of the channel, i.e. the out port signals of statecharts
	'channel' '[' providedPort=InstancePortReference ']' '-o)-' '[' requiredPort=InstancePortReference ']'
;

BroadcastChannel returns CompositeModel::BroadcastChannel:
	// This is the channel's point of view: input is the input of the channel, i.e. the out port signals of statecharts
	'channel' '[' providedPort=InstancePortReference ']' '-o)-' '[' (requiredPorts+=InstancePortReference) (',' requiredPorts+=InstancePortReference)+ ']'
;

// Interface

Interface returns InterfaceModel::Interface:
	{InterfaceModel::Interface}
	'interface' name=ID ('extends' (parents+=[InterfaceModel::Interface]) (',' (parents+=[InterfaceModel::Interface]))* )? '{'
		((events+=EventDeclaration))*
	'}'
;

enum EventDirection returns InterfaceModel::EventDirection:
	IN = 'in' | OUT = 'out' | INOUT = 'inout'
;

EventDeclaration returns InterfaceModel::EventDeclaration:
	{InterfaceModel::EventDeclaration}
	(direction=EventDirection) (event=Event) 
;

Event returns InterfaceModel::Event:
	{InterfaceModel::Event}
	(persistency=Persistency)? 'event' name=ID ('(' (parameterDeclarations+=ParameterDeclaration) (','(parameterDeclarations+=ParameterDeclaration))* ')')? ('priority' '=' priority=INTEGER)?
;

enum Persistency returns InterfaceModel::Persistency:
	PERSISTENT = 'persistent' | TRANSIENT = 'transient'
;

// Overriding it for the sake of EventParameterReferenceExpression
@Override 
PrimaryExpression returns ConstraintModel::Expression: 
	super |
	EventParameterReferenceExpression |
	StateReferenceExpression
;

StateReferenceExpression returns StateReferenceExpression:
	'in-state' '(' region=[Region] '.' state = [State] ')'
;

EventParameterReferenceExpression returns InterfaceModel::EventParameterReferenceExpression:
	port=[InterfaceModel::Port] '.' event=[InterfaceModel::Event] '::' parameter=[ConstraintModel::ParameterDeclaration];


// Action Language Extensions

@Override 
InlineStatement returns ActionModel::Statement:
	(
		VariableDeclarationStatement |
		ConstantDeclarationStatement |
		
		ReturnStatement |
		BreakStatement |
		
		=> AssignmentStatement |
		
		//Extensions:
		RaiseEventAction |
		SetTimeoutAction |
		
		AssertionStatement |
		ExpressionStatement
	)';'
;

RaiseEventAction returns RaiseEventAction:
	'raise' port=[InterfaceModel::Port] '.' event=[InterfaceModel::Event] ('(' (arguments+=Expression)(',' arguments+=Expression)* ')')?
;

SetTimeoutAction returns SetTimeoutAction:
	'set' timeoutDeclaration=[TimeoutDeclaration] ':=' time=TimeSpecification
;

DeactivateTimeoutAction returns DeactivateTimeoutAction:
	'deactivate' timeoutDeclaration=[TimeoutDeclaration]
;