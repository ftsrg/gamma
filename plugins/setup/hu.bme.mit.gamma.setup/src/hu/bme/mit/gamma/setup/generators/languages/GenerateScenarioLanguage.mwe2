/********************************************************************************
 * Copyright (c) 2018-2021 Contributors to the Gamma project
 *
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * SPDX-License-Identifier: EPL-1.0
 ********************************************************************************/
module hu.bme.mit.gamma.setup.generators.languages.GenerateScenarioLanguage

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*
import org.eclipse.emf.mwe.utils.*
import org.eclipse.emf.ecore.resource.impl.*

var rootPath = "../scenario"

Workflow {
	bean = ResourceSetImpl : resourceSet {}
    bean = StandaloneSetup { resourceSet = resourceSet
    	platformUri = "${rootPath}"
    	scanClassPath = true
    	
    	registerGenModelFile = "platform:/resource/hu.bme.mit.gamma.expression.model/model/expression.genmodel"
    	registerGeneratedEPackage = "hu.bme.mit.gamma.expression.model.ExpressionModelPackage"
    	registerGenModelFile = "platform:/resource/hu.bme.mit.gamma.scenario.model/model/scenario.genmodel"
    	registerGeneratedEPackage = "hu.bme.mit.gamma.scenario.model.ScenarioModelPackage"
    }
	
	component = org.eclipse.emf.mwe.utils.DirectoryCleaner {
        directory = "../scenario/hu.bme.mit.gamma.scenario.language/src-gen"
    }
	
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "hu.bme.mit.gamma.scenario.language"
				rootPath = rootPath
				eclipsePlugin = {
					enabled = true
				}
				createEclipseMetaData = true
			}
			code = {
				encoding = "UTF-8"
				lineDelimiter = "\r\n"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
			}
		}
		language = StandardLanguage {
			name = "hu.bme.mit.gamma.scenario.language.ScenarioLanguage"
			fileExtensions = "gsc"
			referencedResource = "platform:/resource/hu.bme.mit.gamma.scenario.model/model/scenario.genmodel"
			fragment = ecore2xtext.Ecore2XtextValueConverterServiceFragment2 auto-inject {}

			formatter = {
				generateStub = false
			}

			serializer = {
				generateStub = false
			}
			validator = {}
		}
	}
}
