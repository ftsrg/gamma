/********************************************************************************
 * Copyright (c) 2018-2021 Contributors to the Gamma project
 *
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * SPDX-License-Identifier: EPL-1.0
 ********************************************************************************/
module hu.bme.mit.gamma.setup.generators.languages.GenerateGenmodelLanguage

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*
import org.eclipse.emf.mwe.utils.*
import org.eclipse.emf.ecore.resource.impl.*

var rootPath = "../core"

Workflow {
	bean = ResourceSetImpl : resourceSet {}
    bean = StandaloneSetup { resourceSet = resourceSet
    	platformUri = "${rootPath}"
    	scanClassPath = true
    	
    	registerGenModelFile = "platform:/resource/hu.bme.mit.gamma.expression.model/model/expression.genmodel"
    	registerGeneratedEPackage = "hu.bme.mit.gamma.expression.model.ExpressionModelPackage"
    	registerGenModelFile = "platform:/resource/hu.bme.mit.gamma.action.model/model/action.genmodel"
    	registerGeneratedEPackage = "hu.bme.mit.gamma.action.model.ActionModelPackage"
    	registerGenModelFile = "platform:/resource/hu.bme.mit.gamma.statechart.model/model/statechart.genmodel"
    	registerGeneratedEPackage = "hu.bme.mit.gamma.statechart.statechart.StatechartModelPackage"
    	registerGeneratedEPackage = "hu.bme.mit.gamma.statechart.interface_.InterfaceModelPackage"
    	registerGeneratedEPackage = "hu.bme.mit.gamma.statechart.composite.CompositeModelPackage"
    	registerGeneratedEPackage = "hu.bme.mit.gamma.statechart.phase.PhaseModelPackage"
    	registerGeneratedEPackage = "hu.bme.mit.gamma.statechart.contract.ContractModelPackage"
    	registerGenModelFile = "platform:/resource/hu.bme.mit.gamma.trace.model/model/trace.genmodel"
    	registerGeneratedEPackage = "hu.bme.mit.gamma.trace.model.TraceModelPackage"
    	registerGenModelFile = "platform:/resource/hu.bme.mit.gamma.property.model/model/property.genmodel"
    	registerGeneratedEPackage = "hu.bme.mit.gamma.property.model.PropertyModelPackage"
    	registerGenModelFile = "platform:/resource/hu.bme.mit.gamma.genmodel.model/model/genmodel.genmodel"
    	registerGeneratedEPackage = "hu.bme.mit.gamma.genmodel.model.GenmodelModelPackage"
    }
    
    component = org.eclipse.emf.mwe.utils.DirectoryCleaner {
        directory = "../core/hu.bme.mit.gamma.genmodel.language/src-gen"
    }
    
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "hu.bme.mit.gamma.genmodel.language"
				rootPath = rootPath
				eclipsePlugin = {
					enabled = true
				}
				createEclipseMetaData = true
			}
			code = {
				encoding = "UTF-8"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
			}
		}
		language = StandardLanguage {
			referencedResource = "platform:/resource/hu.bme.mit.gamma.expression.language/src/hu/bme/mit/gamma/expression/language/ExpressionLanguage.xtext"
			name = "hu.bme.mit.gamma.genmodel.language.GenModel"
			fileExtensions = "ggen"
			
			fragment = ecore2xtext.Ecore2XtextValueConverterServiceFragment2 auto-inject {}

			fragment = org.eclipse.xtext.generator.adapter.FragmentAdapter {
				fragment = org.eclipse.xtext.generator.ecore2xtext.FormatterFragment {}
			}

			serializer = {
				generateStub = false
			}
			validator = {
				// composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
			}
		}
	}
}
