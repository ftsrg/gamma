/********************************************************************************
 * Copyright (c) 2018-2021 Contributors to the Gamma project
 *
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * SPDX-License-Identifier: EPL-1.0
 ********************************************************************************/
package hu.bme.mit.gamma.lowlevel.xsts.transformation.patterns

import epackage "http://www.mit.bme.hu/gamma/expression/Model"
import epackage "http://www.mit.bme.hu/gamma/xSTS/Model"

private pattern referencedVariables(reference : DirectReferenceExpression, variable : VariableDeclaration) {
	DirectReferenceExpression.declaration(reference, variable);
}

pattern assignmentActions(action : AbstractAssignmentAction,
		reference : DirectReferenceExpression, variable : VariableDeclaration) {
	AbstractAssignmentAction.lhs(action, reference);
	find referencedVariables(reference, variable);
}

private pattern readVariables(variable : VariableDeclaration) {
	find referencedVariables(reference, variable);
	neg find assignmentActions(_, reference, variable);
}

pattern notReadVariables(variable : VariableDeclaration) {
	neg find readVariables(variable);
}
