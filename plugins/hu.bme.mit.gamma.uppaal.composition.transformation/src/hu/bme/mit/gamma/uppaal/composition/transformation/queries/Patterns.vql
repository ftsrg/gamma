/********************************************************************************
 * Copyright (c) 2018 Contributors to the Gamma project
 *
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * SPDX-License-Identifier: EPL-1.0
 ********************************************************************************/
package hu.bme.mit.gamma.uppaal.composition.transformation.queries

import "http://www.mit.bme.hu/gamma/constraint/Model" 
import "http://www.mit.bme.hu/gamma/statechart/Model"
import "http://www.mit.bme.hu/gamma/statechart/Model/Composite"
import "http://www.mit.bme.hu/gamma/statechart/Model/Interface"

pattern parameterizedInstances(instance : ComponentInstance) {
	ComponentInstance.parameters(instance, _); 
}

pattern simpleInstances(instance : SynchronousComponentInstance, statechart : StatechartDefinition) {
	SynchronousComponentInstance.type(instance, statechart); 
}

pattern instanceContainer(containerInstace : SynchronousComponentInstance, instance : SynchronousComponentInstance) {
	SynchronousComponentInstance.type(containerInstace, type);
	AbstractSynchronousCompositeComponent.components(type, instance);
}

pattern instanceVariables(instance : SynchronousComponentInstance, variable : VariableDeclaration) {
	find simpleInstances(instance, statechart);
	StatechartDefinition.variableDeclarations(statechart, variable);
}

// To filter out events that are not used
private pattern relevantEvents(event : Event) {
	PortEventReference.event(_, event);
} or {
	Port.interfaceRealization.interface.events.event(port, event);
	AnyPortEventReference.port(_, port);
} or {
	RaiseEventAction.event(_, event);
}

private pattern eventDirection(eventDeclaration : EventDeclaration, direction : EventDirection) {
	 EventDeclaration.direction(eventDeclaration, direction);
}

private pattern componentDeclarationEvents(type : Component, port : Port, realizationMode : RealizationMode, eventDeclaration : EventDeclaration, event : Event) {
	Component.ports(type, port);
	Port.interfaceRealization(port, interfaceRealization);
	InterfaceRealization.realizationMode(interfaceRealization, realizationMode);
	InterfaceRealization.interface(interfaceRealization, interface);
	Interface.events(interface, eventDeclaration);
	EventDeclaration.event(eventDeclaration, event);
}

pattern inputInstanceEvents(instance : SynchronousComponentInstance, port : Port, event : Event) {
	find simpleInstances(instance, statechart);
	find componentDeclarationEvents(statechart, port, RealizationMode::PROVIDED, eventDeclaration, event);
	// OUT events are NOT returned
	neg find eventDirection(eventDeclaration, EventDirection::OUT);
} or {
	find simpleInstances(instance, statechart);
	find componentDeclarationEvents(statechart, port, RealizationMode::REQUIRED, eventDeclaration, event);
	// IN events are NOT returned
	neg find eventDirection(eventDeclaration, EventDirection::IN);
}

private pattern outputInstanceEvents(instance : SynchronousComponentInstance, port : Port, event : Event) {
	find simpleInstances(instance, statechart);
	find componentDeclarationEvents(statechart, port, RealizationMode::PROVIDED, eventDeclaration, event);
	// IN events are NOT returned
	neg find eventDirection(eventDeclaration, EventDirection::IN);
} or {
	find simpleInstances(instance, statechart);
	find componentDeclarationEvents(statechart, port, RealizationMode::REQUIRED, eventDeclaration, event);
	// OUT events are NOT returned
	neg find eventDirection(eventDeclaration, EventDirection::OUT);
}

pattern parameteredEvents(event : Event, param : ParameterDeclaration) {
	Event.parameterDeclarations(event, param);
}

private pattern portBindings(composite : CompositeComponent, systemPort : Port, instance : ComponentInstance, instancePort : Port) {
	CompositeComponent.portBindings(composite, portBinding);
	PortBinding.compositeSystemPort(portBinding, systemPort);
	PortBinding.instancePortReference(portBinding, instancePortRef);
	InstancePortReference.instance(instancePortRef, instance);
	InstancePortReference.port(instancePortRef, instancePort);
}

/**
 * Needed in the traversal of the port binding hierarchy, so traversal is made through the correct instances only.
 */
private pattern instPortRefToInstPortRef(superInstPortRef : InstancePortReference, instPortRef : InstancePortReference) {
	InstancePortReference.instance(superInstPortRef, _instance);
	InstancePortReference.port(superInstPortRef, superPort);
	PortBinding.compositeSystemPort(binding, superPort);
	PortBinding.instancePortReference(binding, instPortRef);
}

// This should be used instead of only portToPort
private pattern portToInstancePort(systemPort : Port, instance : ComponentInstance, port : Port) {
	PortBinding.compositeSystemPort(binding, systemPort);
	PortBinding.instancePortReference(binding, superInstancePortRef);
	find instPortRefToInstPortRef+(superInstancePortRef, instancePortRef);
	InstancePortReference.instance(instancePortRef, instance);
	InstancePortReference.port(instancePortRef, port);
} or {
	// Needed here?
	find portBindings(_, systemPort, instance, port);
}

pattern syncSystemInEvents(composite : AbstractSynchronousCompositeComponent, systemPort : Port, instance : SynchronousComponentInstance, port : Port, event : Event) {
	find portBindings(composite, systemPort, instance, port);
	find inputInstanceEvents(instance, port, event);
} or {
	// Finding embedded composite systems and the port of simple instances
	AbstractSynchronousCompositeComponent.ports(composite, systemPort);
	find portToInstancePort(systemPort, instance, port);
	find inputInstanceEvents(instance, port, event);
}

pattern topSyncSystemInEvents(composite : AbstractSynchronousCompositeComponent, systemPort : Port, instance : SynchronousComponentInstance, port : Port, event : Event) {
	find syncSystemInEvents(composite, systemPort, instance, port, event);
	find topSyncComponents(composite);
	// Filtering out unused event
	find relevantEvents(event);
}

private pattern syncSystemOutEvents(composite : AbstractSynchronousCompositeComponent, systemPort : Port, instance : SynchronousComponentInstance, port : Port, event : Event) {
	find portBindings(composite, systemPort, instance, port);
	find outputInstanceEvents(instance, port, event);
} or {
	// Finding embedded composite systems and the port of simple instances
	AbstractSynchronousCompositeComponent.ports(composite, systemPort);
	find portToInstancePort(systemPort, instance, port);
	find outputInstanceEvents(instance, port, event);
}

pattern topSyncSystemOutEvents(composite : AbstractSynchronousCompositeComponent, systemPort : Port, instance : SynchronousComponentInstance, port : Port, event : Event) {
	find topSyncComponents(composite);
	find syncSystemOutEvents(composite, systemPort, instance, port, event);
}

private pattern instanceTopRegions(instance : SynchronousComponentInstance, topRegion : Region) {
	find simpleInstances(instance, statechart);
	StatechartDefinition.regions(statechart, topRegion);
}

pattern instanceRegions(instance : SynchronousComponentInstance, region : Region) {
	find instanceTopRegions(instance, region);
} or {
	find instanceTopRegions(instance, topRegion);
	find regionToSubregion+(topRegion, region);
}

pattern toLowerInstanceTransitions(instance : SynchronousComponentInstance, source : StateNode, target : StateNode, transition : Transition, region : Region) {
	find simpleInstances(instance, statechart);
	StatechartDefinition.transitions(statechart, transition);
	Transition.sourceState(transition, source);
	Transition.targetState(transition, target);
	find substatesOfCompositeState+(targetAncestor, target);
	Region.stateNodes(region, targetAncestor);
	Region.stateNodes(region, source);
}

pattern toHigherInstanceTransitions(instance : SynchronousComponentInstance, source : StateNode, target : StateNode, transition : Transition, region : Region) {
	find simpleInstances(instance, statechart);
	StatechartDefinition.transitions(statechart, transition);
	Transition.sourceState(transition, source);
	Transition.targetState(transition, target);
	find substatesOfCompositeState+(sourceAncestor, source);
	Region.stateNodes(region, sourceAncestor);
	Region.stateNodes(region, target);
}

pattern statechartRegions(statechart : StatechartDefinition, region: Region, name : java String) {
	find topRegions(statechart, region, name);
} or {
	StatechartDefinition.regions(statechart, topRegion);
	find regionToSubregion+(topRegion, region);
	Region.name(region, name);
}

private pattern topRegions(statechart : StatechartDefinition, region : Region, name : java String) {
	StatechartDefinition.regions(statechart, region);
	Region.name(region, name);
}

pattern regionToSubregion(region : Region, subregion : Region) {
	Region.stateNodes(region, state);
	State.regions(state, subregion);
}

private pattern substatesOfCompositeState(compositeState : State, substate : StateNode) {
	State.regions(compositeState, region);
	Region.stateNodes(region, substate);
}

private pattern channelProvided(channel : Channel , outInstance : ComponentInstance, outPort : Port, raisedEvent : Event) {
	Channel.providedPort(channel, providedPortRef);
	find instancePortReferences(providedPortRef, outInstance, outPort, raisedEvent);
}

private pattern simpleChannelRequired(channel : SimpleChannel, inInstance : ComponentInstance, inPort : Port, raisedEvent : Event) {
	SimpleChannel.requiredPort(channel, outputPortRef);
	find instancePortReferences(outputPortRef, inInstance, inPort, raisedEvent);
}

private pattern instancePortReferences(portRef : InstancePortReference, instance : ComponentInstance , port : Port, event : Event) {
	InstancePortReference.instance(portRef, instance);
	InstancePortReference.port(portRef, port);
	Port.interfaceRealization.interface.events.event(port, event);
}

/**
 * An instance port is either bound to a system port or a channel, cannot be bound to both.
 */
private pattern channels(channel : Channel, outInstance : ComponentInstance, outPort : Port, raisedEvent : Event, inInstance : ComponentInstance, inPort : Port) {
	// Events from PROV-Port to REQ-Port
	find channelProvided(channel, outInstance, outPort, raisedEvent);
	SimpleChannel.requiredPort(channel, outputPortRef);
	find instancePortReferences(outputPortRef, inInstance, inPort, raisedEvent);
	// Returning only "semantic" out events
	EventDeclaration.event(eventDeclaration, raisedEvent);
	neg find eventDirection(eventDeclaration, EventDirection::IN);
} or {
	// Events from REQ-Port to POV-Port
	find simpleChannelRequired(channel, outInstance, outPort, raisedEvent);
	SimpleChannel.providedPort(channel, outputPortRef);
	find instancePortReferences(outputPortRef, inInstance, inPort, raisedEvent);
	// Returning only "semantic" out events
	EventDeclaration.event(decl, raisedEvent);
	neg find eventDirection(decl, EventDirection::OUT);
} or {
	// Events from BROADCAST-Port to REQ-Ports
	find channelProvided(channel, outInstance, outPort, raisedEvent);
	BroadcastChannel.requiredPorts(channel, outputPortRef);
	find instancePortReferences(outputPortRef, inInstance, inPort, raisedEvent);
	// Returning only "semantic" out events
	EventDeclaration.event(eventDeclaration, raisedEvent);
	neg find eventDirection(eventDeclaration, EventDirection::IN);
}

// This should be user for finding channel endpoints
private pattern simpleInstanceChannelEndings(channel : Channel, outInstance : SynchronousComponentInstance,
	 outPort : Port, raisedEvent : Event, inInstance : SynchronousComponentInstance, inPort : Port) {
	// Both instances are simple
	find channels(channel, outInstance, outPort, raisedEvent, inInstance, inPort);
	find simpleInstances(outInstance, _statechart1);
	find simpleInstances(inInstance, _statechart2);
} or {
	// Output instance simple, input instance composite	
	find simpleInstances(outInstance, statechart1);
	Component.ports(statechart1, outPort);
	find portToInstancePort(outSytemPort, outInstance, outPort);
	find channels(channel, _outCompositeInstance, outSytemPort, raisedEvent, inInstance, inPort);
	find simpleInstances(inInstance, _statechart2);
} or {
	// Input instance simple, output instance composite
	find simpleInstances(outInstance, _statechart1);	
	find channels(channel, outInstance, outPort, raisedEvent, _inCompositeInstance, inSystemPort);
	find portToInstancePort(inSystemPort, inInstance, inPort);
	Component.ports(statechart2, inPort);
	find simpleInstances(inInstance, statechart2);
} or {
	// Both instances are composite
	find simpleInstances(outInstance, statechart1);
	Component.ports(statechart1, outPort);
	find portToInstancePort(outSystemPort, outInstance, outPort);	
	find channels(channel, _outCompositeInstance, outSystemPort, raisedEvent, _inCompositeInstance, inSystemPort);
	find portToInstancePort(inSystemPort, inInstance, inPort);
	Component.ports(statechart2, inPort);
	find simpleInstances(inInstance, statechart2);
}

// The following patterns should all have simple instance ending for both ends

// State entry event raisings

private pattern raiseEventEntryActions(state : State, entryAction : RaiseEventAction, raisedEvent : Event, port : Port) {
	State.entryActions(state, entryAction);
	RaiseEventAction.event(entryAction, raisedEvent);
	RaiseEventAction.port(entryAction, port);
}

// For the raising of events connected to another instance on state entry
pattern raiseInstanceEventStateEntryActions(state : State, entryAction : RaiseEventAction, outInstance : SynchronousComponentInstance,
	 outPort : Port, raisedEvent : Event, inInstance : SynchronousComponentInstance, inPort : Port) {
	find raiseEventEntryActions(state, entryAction, raisedEvent, outPort);
	find simpleInstanceChannelEndings(_channel, outInstance, outPort, raisedEvent, inInstance, inPort);
}

// For the raising of out events led to the top composite system ports on state entry
private pattern raiseSystemEventStateEntryActions(composite : AbstractSynchronousCompositeComponent, state : State,
	instance : SynchronousComponentInstance, raisedEvent : Event, outPort : Port, entryAction : RaiseEventAction) {
	find raiseEventEntryActions(state, entryAction, raisedEvent, outPort);
	find syncSystemOutEvents(composite, _systemPort, instance, outPort, raisedEvent);
}

// Same for top level sync components
pattern raiseTopSystemEventStateEntryActions(composite : AbstractSynchronousCompositeComponent, state : State,
	instance : SynchronousComponentInstance, raisedEvent : Event, outPort : Port, entryAction : RaiseEventAction) {
	find topSyncComponents(composite);
	find raiseSystemEventStateEntryActions(composite, state, instance, raisedEvent, outPort, entryAction);
}

// State entry event raisings

private pattern raiseEventExitActions(state : State, exitAction : RaiseEventAction, raisedEvent : Event, port : Port) {
	State.exitActions(state, exitAction);
	RaiseEventAction.event(exitAction, raisedEvent);
	RaiseEventAction.port(exitAction, port);
}

// For the raising of events connected to another instance on state exit
pattern raiseInstanceEventStateExitActions(state : State, exitAction : RaiseEventAction, outInstance : SynchronousComponentInstance,
	 outPort : Port, raisedEvent : Event, inInstance : SynchronousComponentInstance, inPort : Port) {
	find raiseEventExitActions(state, exitAction, raisedEvent, outPort);
	find simpleInstanceChannelEndings(_channel, outInstance, outPort, raisedEvent, inInstance, inPort);
}

// For the raising of out events led to the top composite system ports on state exit
pattern raiseSystemEventStateExitActions(composite : AbstractSynchronousCompositeComponent, state : State,
	instance : SynchronousComponentInstance, outPort : Port, raisedEvent : Event, exitAction : RaiseEventAction) {
	find raiseEventExitActions(state, exitAction, raisedEvent, outPort);
	find syncSystemOutEvents(composite, _systemPort, instance, outPort, raisedEvent);
}

// Same for top level sync components
pattern raiseTopSystemEventStateExitActions(composite : AbstractSynchronousCompositeComponent, state : State,
	instance : SynchronousComponentInstance, outPort : Port, raisedEvent : Event, exitAction : RaiseEventAction) {
	find topSyncComponents(composite);
	find raiseSystemEventStateExitActions(composite, state, instance, outPort, raisedEvent, exitAction);
}

// Transition event raisings

private pattern raiseEventTransitionEffects(transition : Transition, eventRaiseAction : RaiseEventAction, raisedEvent : Event, port : Port) {
	Transition.effects(transition, eventRaiseAction);
	RaiseEventAction.event(eventRaiseAction, raisedEvent);
	RaiseEventAction.port(eventRaiseAction, port);
}

// For the raising of events connected to another instance on transitions
pattern raiseInstanceEventOfTransitions(transition : Transition, eventRaiseAction : RaiseEventAction, outInstance : SynchronousComponentInstance,
	 outPort : Port, raisedEvent : Event, inInstance : SynchronousComponentInstance, inPort : Port) {
	find raiseEventTransitionEffects(transition, eventRaiseAction, raisedEvent, outPort);
	find simpleInstanceChannelEndings(_channel, outInstance, outPort, raisedEvent, inInstance, inPort);
}

// For the raising of out events led to the top composite system ports on transitions
private pattern raiseSystemEventOfTransitions(composite : AbstractSynchronousCompositeComponent, transition : Transition,
	instance : SynchronousComponentInstance, outPort : Port, raisedEvent : Event, eventRaiseAction : RaiseEventAction) {
	find raiseEventTransitionEffects(transition, eventRaiseAction, raisedEvent, outPort);
	find syncSystemOutEvents(composite, _systemPort, instance, outPort, raisedEvent);
}

// Same for top level sync components
pattern raiseTopSystemEventOfTransitions(composite : AbstractSynchronousCompositeComponent, transition : Transition,
	instance : SynchronousComponentInstance, outPort : Port, raisedEvent : Event, eventRaiseAction : RaiseEventAction) {
	find topSyncComponents(composite); 
	find raiseSystemEventOfTransitions(composite, transition, instance, outPort, raisedEvent, eventRaiseAction);
}

// Scheduling of synchronous components

private pattern synchronousInstanceContainer(containerInstace : SynchronousComponentInstance, instance : SynchronousComponentInstance) {
	SynchronousComponentInstance.type(containerInstace, type);
	SynchronousCompositeComponent.components(type, instance);
}

pattern queueSwapInstancesOfComposite(topComposite : SynchronousCompositeComponent, instance : SynchronousComponentInstance) {
	// Multiple level composite components
	SynchronousCompositeComponent.components(topComposite, topInstance);
	find synchronousInstanceContainer+(topInstance, instance);
	find simpleInstances(instance, _);
} or {
	// Single level composite components
	SynchronousCompositeComponent.components(topComposite, instance);
	find simpleInstances(instance, _);
}

// Choosing the top level components: sync, wrapper and async

private pattern synchronousInstances(instance : SynchronousComponentInstance, type : SynchronousComponent) {
	SynchronousComponentInstance.type(instance, type);
}

private pattern asynchronousInstances(instance : AsynchronousComponentInstance, type : AsynchronousComponent) {
	AsynchronousComponentInstance.type(instance, type);
}

pattern asynchronousSimpleInstances(instance : AsynchronousComponentInstance, type : SynchronousComponentWrapper) {
	AsynchronousComponentInstance.type(instance, type);
}

private pattern wrappers(wrapper : SynchronousComponentWrapper, sync : SynchronousComponent) {
	SynchronousComponentWrapper.wrappedComponent(wrapper, sync);
}

private pattern topSyncComponents(syncComposite : AbstractSynchronousCompositeComponent) {
	neg find synchronousInstances(_, syncComposite);
}

pattern topUnwrappedSyncComponents(syncComposite : AbstractSynchronousCompositeComponent) {
	neg find synchronousInstances(_, syncComposite);
	neg find wrappers(_, syncComposite);
}

pattern topWrapperComponents(wrapper : SynchronousComponentWrapper, composite : AbstractSynchronousCompositeComponent) {
	neg find asynchronousInstances(_, wrapper);
	SynchronousComponentWrapper.wrappedComponent(wrapper, composite);
}

pattern topAsyncCompositeComponents(asyncComposite : AsynchronousCompositeComponent) {
	neg find asynchronousInstances(_, asyncComposite);
}

// Choosing all the asynchronous wrapper component

pattern simpleWrapperInstances(instance : AsynchronousComponentInstance, wrapper : SynchronousComponentWrapper, composite : AbstractSynchronousCompositeComponent) {
	AsynchronousComponentInstance.type(instance, wrapper);
	SynchronousComponentWrapper.wrappedComponent(wrapper, composite);
}

// In events of wrappers

pattern distinctWrapperInEvents(wrapper : SynchronousComponentWrapper, port : Port, event : Event) {
	// This is not delegated, these events and ports are on the wrapper
	find wrappers(wrapper, _composite);
	find componentDeclarationEvents(wrapper, port, RealizationMode::PROVIDED, eventDeclaration, event);
	// OUT events are NOT returned
	neg find eventDirection(eventDeclaration, EventDirection::OUT);
} or {
	// This is not delegated, these events and ports are on the wrapper
	find wrappers(wrapper, _composite);
	find componentDeclarationEvents(wrapper, port, RealizationMode::REQUIRED, eventDeclaration, event);
	// IN events are NOT returned
	neg find eventDirection(eventDeclaration, EventDirection::IN);
}

pattern wrapperInEvents(wrapper : SynchronousComponentWrapper, port : Port, event : Event) {
	// This is delegated down to the events and ports of sync instances
	find wrappers(wrapper, composite);
	find syncSystemInEvents(composite, port, _instance, _instancePort, event);
} or {
	find distinctWrapperInEvents(wrapper, port, event);
}

pattern wrapperInstanceInEvents(instance : AsynchronousComponentInstance, port : Port, event : Event) {
	find asynchronousSimpleInstances(instance, wrapper);
	find wrapperInEvents(wrapper, port, event);
}

// In events of asnync composites

private pattern asyncSystemInEvents(composite : AsynchronousCompositeComponent, systemPort : Port,
	instance : AsynchronousComponentInstance, port : Port, event : Event) {
	find portBindings(composite, systemPort, instance, port);
	find wrapperInstanceInEvents(instance, port, event);
} or {
	// Finding embedded composite systems and the port of simple instances
	AsynchronousCompositeComponent.ports(composite, systemPort);
	find portToInstancePort(systemPort, instance, port);
	find wrapperInstanceInEvents(instance, port, event);
}

pattern topAsyncSystemInEvents(composite : AsynchronousCompositeComponent, systemPort : Port,
	instance : AsynchronousComponentInstance, port : Port, event : Event) {
	find topAsyncCompositeComponents(composite);
	find asyncSystemInEvents(composite, systemPort, instance, port, event);
}

private pattern wrapperPorts(wrapper : SynchronousComponentWrapper, port : Port) {
	SynchronousComponentWrapper.ports(wrapper, port);
} or {
	SynchronousComponentWrapper.wrappedComponent.ports(wrapper, port);
}

// This should be user for finding channel endpoints
private pattern simpleWrapperInstanceChannelEndings(channel : Channel, outInstance : AsynchronousComponentInstance,
	 outPort : Port, raisedEvent : Event, inInstance : AsynchronousComponentInstance, inPort : Port) {
	// Both instances are simple
	find channels(channel, outInstance, outPort, raisedEvent, inInstance, inPort);
	find simpleWrapperInstances(outInstance, _, _);
	find simpleWrapperInstances(inInstance, _, _);
} or {
	// Input instance simple, output instance composite	
	find simpleWrapperInstances(outInstance, outWrapper, _);
	find wrapperPorts(outWrapper, outPort);
	find portToInstancePort(outSytemPort, outInstance, outPort);
	find channels(channel, _outCompositeInstance, outSytemPort, raisedEvent, inInstance, inPort);
	find simpleWrapperInstances(inInstance, _, _);
} or {
	// Output instance simple, input instance composite
	find simpleWrapperInstances(outInstance, _, _);
	find channels(channel, outInstance, outPort, raisedEvent, _inCompositeInstance, inSystemPort);
	find portToInstancePort(inSystemPort, inInstance, inPort);
	find wrapperPorts(inWrapper, inPort);
	find simpleWrapperInstances(inInstance, inWrapper, _);
} or {
	// Both instances are composite
	find simpleWrapperInstances(outInstance, outWrapper, _);
	find wrapperPorts(outWrapper, outPort);
	find portToInstancePort(outSystemPort, outInstance, outPort);	
	find channels(channel, _outCompositeInstance, outSystemPort, raisedEvent, _inCompositeInstance, inSystemPort);
	find portToInstancePort(inSystemPort, inInstance, inPort);
	find wrapperPorts(inWrapper, inPort);
	find simpleWrapperInstances(inInstance, inWrapper, _);
}

pattern eventsIntoMessageQueues(outInstance : AsynchronousComponentInstance, outPort : Port, raisedEvent : Event,
	inInstance : AsynchronousComponentInstance, inPort : Port, queue : MessageQueue) {
	find simpleWrapperInstanceChannelEndings(_, outInstance, outPort, raisedEvent, inInstance, inPort);
	find instanceMessageQueues(inInstance, wrapper, queue);
	find queuesOfEvents(wrapper, queue, inPort, raisedEvent);
}

// Message queues

pattern topMessageQueues(wrapper : SynchronousComponentWrapper, queue : MessageQueue) {
	neg find asynchronousSimpleInstances(_, wrapper);
	SynchronousComponentWrapper.messageQueues(wrapper, queue);
}

pattern instanceMessageQueues(instance : AsynchronousComponentInstance, wrapper : SynchronousComponentWrapper, queue : MessageQueue) {
	find asynchronousSimpleInstances(instance, wrapper);
	SynchronousComponentWrapper.messageQueues(wrapper, queue);
}

// Events to message queues

private pattern parentInterfaces(interface : Interface, parent : Interface) {
	Interface.parents(interface, parent);
}

private pattern eventsOfPorts(port : Port, event : Event) {
	Port.interfaceRealization.interface(port, interface);
	Interface.events.event(interface, event);
} or {
	Port.interfaceRealization.interface(port, interface);
	find parentInterfaces+(interface, parentInterface);
	Interface.events.event(parentInterface, event);
}

pattern queuesOfClocks(wrapper : SynchronousComponentWrapper, queue : MessageQueue, clock : Clock) {
	SynchronousComponentWrapper.messageQueues(wrapper, queue);
	MessageQueue.eventReference(queue, eventReference);
	ClockTickReference.clock(eventReference, clock);
}

pattern queuesOfEvents(wrapper : SynchronousComponentWrapper, queue : MessageQueue, port : Port, event : Event) {
	SynchronousComponentWrapper.messageQueues(wrapper, queue);
	MessageQueue.eventReference(queue, eventReference);
	PortEventReference.port(eventReference, port);
	find eventsOfPorts(port, event);
	PortEventReference.event(eventReference, event);
} or {
	SynchronousComponentWrapper.messageQueues(wrapper, queue);
	MessageQueue.eventReference(queue, eventReference);
	AnyPortEventReference.port(eventReference, port);
	find eventsOfPorts(port, event);
}

pattern queuesOfEventsOfWrappedComponents(wrapper : SynchronousComponentWrapper, queue  : MessageQueue) {
	find queuesOfEvents(wrapper, queue, port, _event);
	SynchronousComponentWrapper.wrappedComponent(wrapper, syncComp);
	Component.ports(syncComp, port);
}

// Priority between queues

pattern queuePriorities(wrapper : SynchronousComponentWrapper, lowerPriotityQueue : MessageQueue, higherPriotityQueue : MessageQueue) {
	SynchronousComponentWrapper.messageQueues(wrapper, lowerPriotityQueue);
	SynchronousComponentWrapper.messageQueues(wrapper, higherPriotityQueue);
	MessageQueue.priority(lowerPriotityQueue, lowerPriority);
	MessageQueue.priority(higherPriotityQueue, higherPriority);
	check (lowerPriority < higherPriority);
}

// Control specification

private pattern runOnceControl(wrapper : SynchronousComponentWrapper, eventReference : EventReference) {
	SynchronousComponentWrapper.controlSpecifications(wrapper, controlSpecification);
	ControlSpecification.trigger(controlSpecification, trigger);
	ControlSpecification.controlFunction(controlSpecification, ControlFunction::RUN_ONCE);
	EventTrigger.eventReference(trigger, eventReference);
}

pattern runOnceEventControl(wrapper : SynchronousComponentWrapper, port : Port, event : Event) {
	find wrapperInEvents(wrapper, port, event);
	find runOnceControl(wrapper, eventReference);
	PortEventReference.port(eventReference, port);
	find eventsOfPorts(port, event);
	PortEventReference.event(eventReference, event);
} or {
	find wrapperInEvents(wrapper, port, event);
	find runOnceControl(wrapper, eventReference);
	AnyPortEventReference.port(eventReference, port);
	find eventsOfPorts(port, event);
}

pattern unusedWrapperEvents(wrapper : SynchronousComponentWrapper, port : Port, event : Event) {
	SynchronousComponentWrapper.ports(wrapper, port);
	find eventsOfPorts(port, event);
	neg find runOnceEventControl(wrapper, port, event);
}

pattern runOnceClockControl(wrapper : SynchronousComponentWrapper, queue : MessageQueue, clock : Clock) {
	find runOnceControl(wrapper, eventReference);
	ClockTickReference.clock(eventReference, clock);
	find queuesOfClocks(wrapper, queue, clock);
}

// Max timeout
pattern timeoutValues(valueExp : Expression, unit : TimeUnit) {
	TimeSpecification.value(timeSpec, valueExp);
	TimeSpecification.unit(timeSpec, unit);
}

